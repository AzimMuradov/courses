\documentclass[xetex,mathserif,serif]{beamer}
\usepackage{polyglossia}
\setdefaultlanguage[babelshorthands=true]{russian}
\usepackage{minted}
\usepackage{tabu}
\usepackage{algorithm2e}

\useoutertheme{infolines}

\usepackage{fontspec}
\setmainfont{FreeSans}
\newfontfamily{\russianfonttt}{FreeSans}

\usepackage{textpos}
\setlength{\TPHorizModule}{1cm}
\setlength{\TPVertModule}{1cm}

\setbeamertemplate{blocks}[rounded][shadow=false]

\setbeamercolor*{block title example}{fg=green!50!black,bg=green!15}
\setbeamercolor*{block body example}{fg=black,bg=green!5}

\tabulinesep=0.5mm

\hypersetup{colorlinks=true, linkcolor=blue, citecolor=blue, filecolor=blue, urlcolor=blue, pdftitle=1, pdfauthor=, pdfsubject=, pdfkeywords=}

\newcommand{\attribution}[1] {
    \begin{flushright}\begin{scriptsize}\textcolor{gray}{#1}\end{scriptsize}\end{flushright}
}

\title{Синтаксический анализ на F\#}
\subtitle{Часть 2: FParsec vs FsLex/FsYacc}
\author{Юрий Литвинов}
\date{16.04.2021г}

\begin{document}
    
    \frame{\titlepage}

    \section{Введение}
    
    \begin{frame}
        \frametitle{Арифметический интерпретатор}
        Задача: разработать интерпретатор арифметических выражений
        \begin{itemize}
            \item Должны поддерживаться 
            \begin{itemize}
                \item +, -, *, / (с приоритетами операций)
                \item Унарный минус
                \item Скобки
                \item Целые числа
            \end{itemize}
            \item По входной строке надо явно построить AST
            \item По построенному AST вычислить выражение
        \end{itemize}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Грамматика}
        \begin{minted}{bnf}
E ::= E + E
    | E - E
    | E * E
    | E / E
    | -E
    | (E)
    | NUMBER

NUMBER ::= [0..9]+
        \end{minted}
    \end{frame}

    \section{FParsec}

    \subsection{Подготовительная работа}

    \begin{frame}[fragile]
        \frametitle{Подготовительная работа}
        \begin{itemize}
            \item Создаём проект
            \item Добавляем ссылку на FParsec в проект
            \item Убеждаемся, что всё работает
        \end{itemize}
        \begin{minted}{fsharp}
open FParsec

[<EntryPoint>]
let main argv =
    let result = "1.23" |> (run pfloat)
    printfn "%A" result
    0 
        \end{minted}
    \end{frame}

    \subsection{AST}

    \begin{frame}[fragile]
        \frametitle{Представление AST}
        \begin{minted}{fsharp}
type Expression =
    | Plus of Expression * Expression
    | Minus of Expression * Expression
    | Multiplication of Expression * Expression
    | Division of Expression * Expression
    | Negation of Expression
    | Number of int
        \end{minted}
    \end{frame}

    \subsection{Разбор чисел}

    \begin{frame}[fragile]
        \frametitle{Начнём с Number}
        \begin{minted}{fsharp}
let number = digit

let testInput = "9"
printfn "%A" (testInput |> run number)

let testInput = "12"
printfn "%A" (testInput |> run number)
        \end{minted}

        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
Success: '9'
Success: '1'
            \end{minted}
        \end{exampleblock}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Позитивное замыкание, правильный разбор числа}
        \begin{minted}{fsharp}
let number = many1 digit

let testInput = "9"
printfn "%A" (testInput |> run number)

let testInput = "12"
printfn "%A" (testInput |> run number)
        \end{minted}

        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
Success: ['9']
Success: ['1'; '2']
            \end{minted}
        \end{exampleblock}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Делаем узел дерева}
        \begin{minted}{fsharp}
let number = 
    many1 digit 
    |>> (List.fold (fun acc x -> acc * 10 + int (x.ToString())) 0 >> Number)

let testInput = "9"
printfn "%A" (testInput |> run number)

let testInput = "12"
printfn "%A" (testInput |> run number)
        \end{minted}

        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
Success: Number 9
Success: Number 12
            \end{minted}
        \end{exampleblock}
    \end{frame}

    \subsection{Разбор унарного минуса}

    \begin{frame}[fragile]
        \frametitle{Рекурсивные правила}
        \begin{minted}{fsharp}
let expression, expressionRef = createParserForwardedToRef()
let negation = pchar '-' >>. expression |>> Negation
expressionRef := choice [negation; number]

let testInput = "-9"
printfn "%A" (testInput |> run expression)

let testInput = "--12"
printfn "%A" (testInput |> run expression)
        \end{minted}

        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
Success: Negation (Number 9)
Success: Negation (Negation (Number 12))
            \end{minted}
        \end{exampleblock}
    \end{frame}

    \subsection{Пробелы}

    \begin{frame}[fragile]
        \frametitle{Победим пробелы}
        \begin{minted}{fsharp}
let expression, expressionRef = createParserForwardedToRef()
let negation = 
    pchar '-' .>> spaces >>. expression .>> spaces |>> Negation
expressionRef := choice [negation; number]

let testInput = "- 9"
printfn "%A" (testInput |> run expression)
        \end{minted}

        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
Success: Negation (Number 9)
            \end{minted}
        \end{exampleblock}
    \end{frame}

    \subsection{Разбор сложения}

    \begin{frame}[fragile]
        \frametitle{Сложение!}
        \framesubtitle{Наивный подход-1}
        \begin{minted}{fsharp}
let expression, expressionRef = createParserForwardedToRef()
let negation = 
    pchar '-' .>> spaces >>. expression .>> spaces |>> Negation
let plus = expression .>> pchar '+' .>>. expression |>> Plus

expressionRef := choice [negation; number; plus]

let testInput = "1 + 2"
printfn "%A" (testInput |> run expression)
        \end{minted}

        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
Success: Number 1
            \end{minted}
        \end{exampleblock}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Сложение!}
        \framesubtitle{Наивный подход-2}
        \begin{minted}{fsharp}
let expression, expressionRef = createParserForwardedToRef()
let negation = 
    pchar '-' .>> spaces >>. expression .>> spaces |>> Negation
let plus = expression .>> pchar '+' .>>. expression |>> Plus

expressionRef := choice [negation; plus; number]

let testInput = "1 + 2"
printfn "%A" (testInput |> run expression)
        \end{minted}

        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
Stack overflow.
            \end{minted}
        \end{exampleblock}
        \begin{textblock}{2}(9,-3.5)
            \includegraphics[width=\textwidth]{hmm.png}
        \end{textblock}
    \end{frame}

    \subsection{Факторизация}

    \begin{frame}[fragile]
        \frametitle{Факторизуем грамматику}
        \begin{minted}{bnf}
E ::= PRIMARY E'

E' ::= + PRIMARY E'
    | - PRIMARY E'
    | * PRIMARY E'
    | / PRIMARY E'
    | e

PRIMARY ::= -E
    | (E)
    | NUMBER

NUMBER ::= [0..9]+
        \end{minted}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Перепишем парсер}
        \begin{minted}{fsharp}
let expression, expressionRef = createParserForwardedToRef()
let negation = 
    pchar '-' .>> spaces >>. expression .>> spaces |>> Negation
let brackets =
     pchar '(' .>> spaces >>. expression .>> spaces .>> pchar ')' .>> spaces 

let primary = 
    negation
    <|> brackets
    <|> number

let expression', expression'Ref  = createParserForwardedToRef()
expression'Ref := pchar '+' >>. primary .>>. expression' |>> ???
        \end{minted}
        AST строить неудобно!
    \end{frame}

    \subsection{Parse tree}

    \begin{frame}[fragile]
        \frametitle{Введём промежуточное представление дерева}
        \framesubtitle{Parse tree}
        \begin{minted}{fsharp}
type Primary =
    | Negation of E
    | Brackets of E
    | Number of int
and E = 
    | E of Primary * E'
and E' =
    | Plus of Primary * E'
    | Minus of Primary * E'
    | Multiplication of Primary * E'
    | Division of Primary * E'
    | Epsilon
        \end{minted}
    \end{frame}

    \subsection{Факторизованный парсер}

    \begin{frame}[fragile]
        \frametitle{Теперь уже перепишем парсер (1)}
        \framesubtitle{Чтобы он строил Parse tree}
        \begin{minted}{fsharp}
let e, eRef = createParserForwardedToRef()

let negation = pchar '-' .>> spaces >>. e .>> spaces |>> Negation
let brackets = 
    pchar '(' .>> spaces >>. e .>> spaces .>> pchar ')' .>> spaces
    |>> Brackets

let primary = 
    negation
    <|> brackets
    <|> number

        \end{minted}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Теперь уже перепишем парсер (2)}
        \framesubtitle{E' и всё вместе}
        \begin{small}
            \begin{minted}{fsharp}
let e', e'Ref  = createParserForwardedToRef()
e'Ref := 
    (pchar '+' >>. spaces >>. primary .>> spaces .>>. e' |>> Plus)
    <|> (pchar '-' >>. spaces >>. primary .>> spaces .>>. e' |>> Minus)
    <|> (pchar '*' >>. spaces >>. primary .>> spaces .>>. e' |>> Multiplication)
    <|> (pchar '/' >>. spaces >>. primary .>> spaces .>>. e' |>> Division)
    <|> preturn Epsilon

eRef := primary .>> spaces .>>. e' |>> E

let testInput = "1 + 2"
printfn "%A" (testInput |> run e)
            \end{minted}

            \begin{exampleblock}{Вывод}
                \begin{minted}{fsharp}
Success: E (Number 1,Plus (Number 2,Epsilon))
                \end{minted}
            \end{exampleblock}
        \end{small}
    \end{frame}

    \subsection{Свои комбинаторы}

    \begin{frame}[fragile]
        \frametitle{Небольшой рефакторинг}
        \begin{minted}{fsharp}
let (!) parser = parser .>> spaces

let e', e'Ref  = createParserForwardedToRef()
e'Ref := 
    (!(pchar '+') >>. !primary .>>. !e' |>> Plus)
    <|> (!(pchar '-') >>. !primary .>>. !e' |>> Minus)
    <|> (!(pchar '*') >>. !primary .>>. !e' |>> Multiplication)
    <|> (!(pchar '/') >>. !primary .>>. !e' |>> Division)
    <|> preturn Epsilon

eRef := !primary .>>. !e' .>> eof |>> E
        \end{minted}
    \end{frame}

    \subsection{Приоритет операций}

    \begin{frame}[fragile]
        \frametitle{Приоритет операций, проблема}
        \begin{small}
            \begin{minted}{fsharp}
let testInput = "1 + 2 * 3"
printfn "%A" (testInput |> run e)

let testInput = "1 * 2 + 3"
printfn "%A" (testInput |> run e)
            \end{minted}

            \begin{exampleblock}{Вывод}
                \begin{minted}{fsharp}
Success: E (Number 1,Plus (Number 2,Multiplication (Number 3,Epsilon)))
Success: E (Number 1,Multiplication (Number 2,Plus (Number 3,Epsilon)))
                \end{minted}
            \end{exampleblock}
            Алгоритм сортировочной станции? Нет! У нас есть вся мощь формальных языков и библиотека парсер-комбинаторов
        \end{small}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Ещё раз подправим грамматику}
        \begin{small}
            \begin{minted}{bnf}
E ::= TERM E'

E' ::= + TERM E'
    | - TERM E'
    | e

TERM ::= FACTOR TERM'

TERM' = 
    | * FACTOR TERM'
    | / FACTOR TERM'
    | e

FACTOR ::= -E
    | (E)
    | NUMBER

NUMBER ::= [0..9]+
            \end{minted}
        \end{small}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Приведём Parse Tree в соответствие}
        \begin{small}
            \begin{minted}{fsharp}
type E =
    | E of Term * E'
and E' =
    | Plus of Term * E'
    | Minus of Term * E'
    | Epsilon
and Term = 
    | Term of Factor * Term'
and Term' =
    | Multiplication of Factor * Term'
    | Division of Factor * Term'
    | Epsilon
and Factor =
    | Negation of E
    | Brackets of E
    | Number of int
            \end{minted}
        \end{small}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{И сам парсер}
        \begin{scriptsize}
            \begin{minted}{fsharp}
let e, eRef = createParserForwardedToRef()

let factor = !(pchar '-') >>. !e |>> Negation
             <|> (!(pchar '(') >>. !e .>> !(pchar ')') |>> Brackets)
             <|> number

let term', term'Ref = createParserForwardedToRef()

term'Ref := !(pchar '*') >>. !factor .>>. !term' |>> Multiplication
            <|> (!(pchar '/') >>. !factor .>>. !term' |>> Division)
            <|> preturn Epsilon

let term = !factor .>>. !term' |>> Term

let e', e'Ref  = createParserForwardedToRef()

e'Ref :=
    !(pchar '+') >>. !term .>>. !e' |>> Plus
    <|> (!(pchar '-') >>. !term .>>. !e' |>> Minus)
    <|> preturn E'.Epsilon

eRef := !term .>>. !e' |>> E
            \end{minted}
        \end{scriptsize}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Теперь}
        \begin{minted}{fsharp}
let testInput = "1 + 2 * 3"
printfn "%A" (testInput |> run e)

let testInput = "1 * 2 + 3"
printfn "%A" (testInput |> run e)
        \end{minted}

        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
Success: E (Term (Number 1,Epsilon),
    Plus (Term (Number 2,Multiplication (Number 3,Epsilon)),Epsilon))
Success: E (Term (Number 1,Multiplication (Number 2,Epsilon)),
    Plus (Term (Number 3,Epsilon),Epsilon))
            \end{minted}
        \end{exampleblock}
        Сложнее, но тут уже получилась некоторая структура
    \end{frame}

    \subsection{Построение AST}

    \begin{frame}[fragile]
        \frametitle{Построим AST по Parse Tree}
        \framesubtitle{Сначала Factor}
        \begin{minted}{fsharp}
let rec buildAST expr =
    let buildFactor = function
    | Negation(e) -> Expression.Negation(buildAST e)
    | Brackets(e) -> buildAST e
    | Number(x) -> Expression.Number(x)

    ()
        \end{minted}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Построим AST по Parse Tree}
        \framesubtitle{Теперь термы}
        \begin{minted}{fsharp}
let rec buildTerm' acc = function
| Multiplication(factor, rest) -> 
    buildTerm' (Expression.Multiplication(acc, buildFactor factor)) rest
| Division(factor, rest) -> 
    buildTerm' (Expression.Division(acc, buildFactor factor)) rest
| Epsilon -> acc

let buildTerm (Term(factor, rest)) = buildTerm' (buildFactor factor) rest
        \end{minted}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Построим AST по Parse Tree}
        \framesubtitle{А теперь и всё выражение}
        \begin{minted}{fsharp}
let rec buildE' acc = function
| Plus(factor, rest) -> 
    buildE' (Expression.Plus(acc, buildTerm factor)) rest
| Minus(factor, rest) -> 
    buildE' (Expression.Minus(acc, buildTerm factor)) rest
| E'.Epsilon -> acc

let buildE (E(term, rest)) = buildE' (buildTerm term) rest

buildE expr
        \end{minted}
    \end{frame}

    \subsection{Что получилось}

    \begin{frame}[fragile]
        \frametitle{Потестим}
        \begin{minted}{fsharp}
let testInput = "1 * 2 + 3"

let result = testInput |> run e
printfn "%A" result

match result with
| Success(result, _, _) -> printfn "%A" <| buildAST result
| _ -> printfn "%A" result
        \end{minted}

        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
Success: E (Term (Number 1,Multiplication (Number 2,Epsilon)),
    Plus (Term (Number 3,Epsilon),Epsilon))
Plus (Multiplication (Number 1,Number 2),Number 3)
            \end{minted}
        \end{exampleblock}
    \end{frame}

    \subsection{Что дальше}

    \begin{frame}
        \frametitle{Что дальше}
        \begin{small}
            \begin{itemize}
                \item А считать выражение по такому дереву мы уже умеем
                \item Что в итоге получилось: \url{https://gist.github.com/yurii-litvinov/3b8b9e9328e06ac49d15481ba2cb3684}
                \item Что ещё умеет FParsec: \url{https://www.quanttec.com/fparsec/tutorial.html}
                \item Полное описание API библиотеки: \url{https://www.quanttec.com/fparsec/reference/}
                \item Монады! 
                
                \url{https://www.quanttec.com/fparsec/users-guide/where-is-the-monad.html}
                \item Как на самом деле парсить арифметические выражения: 
                
                \url{https://www.quanttec.com/fparsec/reference/operatorprecedenceparser.html}
            \end{itemize}
        \end{small}
    \end{frame}

    \section{FsLex/FsYacc}

    \subsection{Минимальный проект, лексер}

    \begin{frame}[fragile]
        \frametitle{FsLex/FsYacc, подготовительная работа}
        \begin{itemize}
            \item Создаём проект
            \item Добавляем ссылку на nuget-пакет FsLexYacc
            \item Создаём новый файл, Lexer.fsl
            \item Пишем туда
        \end{itemize}
        \begin{small}
            \begin{minted}{fsharp}
{ 
open FSharp.Text.Lexing
open System

let lexeme = LexBuffer<_>.LexemeString
}

let digit = ['0'-'9']

rule token = parse
| digit+  { Int32.Parse(lexeme lexbuf) }
            \end{minted}
        \end{small}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{FsLex/FsYacc, подготовительная работа (2)}
        \begin{itemize}
            \item Добавляем в проектный файл описание лексера
        \end{itemize}
        \begin{scriptsize}
            \begin{minted}{xml}
<Project Sdk="Microsoft.NET.Sdk">

<PropertyGroup>
    <OutputType>Exe</OutputType>
    <TargetFramework>netcoreapp3.1</TargetFramework>
</PropertyGroup>

<ItemGroup>
    <FsLex Include="Lexer.fsl">
        <OtherFlags>--module Lexer --unicode</OtherFlags>
    </FsLex>
    <Compile Include="Program.fs" />
</ItemGroup>

<ItemGroup>
    <PackageReference Include="FsLexYacc" Version="10.0.0" />
</ItemGroup>

</Project>
            \end{minted}
        \end{scriptsize}
    \end{frame}
    
    \begin{frame}[fragile]
        \frametitle{FsLex/FsYacc, подготовительная работа (3)}
        \begin{itemize}
            \item Компилируем проект и добавляем в проект появившийся Lexer.fs
            \item Почему так извращённо --- потому что обычно для сборки используют fake
        \end{itemize}
        \begin{scriptsize}
            \begin{minted}{xml}
<ItemGroup>
<Compile Include="Lexer.fs" />
<FsLex Include="Lexer.fsl">
    <OtherFlags>--module Lexer --unicode</OtherFlags>
</FsLex>
<Compile Include="Program.fs" />
</ItemGroup>
            \end{minted}
        \end{scriptsize}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{FsLex/FsYacc, подготовительная работа (4)}
        \begin{itemize}
            \item Пишем точку входа
        \end{itemize}
        \begin{minted}{fsharp}
open FSharp.Text.Lexing

[<EntryPoint>]
let main argv =
    let testInput = "9"

    let lexbuf = LexBuffer<char>.FromString testInput
    let tokens = Lexer.token lexbuf

    printfn "%A" tokens
    0 
        \end{minted}

        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
9
            \end{minted}
        \end{exampleblock}
    \end{frame}

    \subsection{Минимальный проект, парсер}

    \begin{frame}[fragile]
        \frametitle{Теперь то же самое с парсером}
        \begin{itemize}
            \item Создаём Parser.fsy
        \end{itemize}
        \begin{minted}{text}
%{
%}

%token <int> INT

%start start
%type <int> start

%%

start: INT { $1 }
        \end{minted}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Добавляем в проектный файл, руками}
        \framesubtitle{До лексера!}
        \begin{scriptsize}
            \begin{minted}{xml}
<Project Sdk="Microsoft.NET.Sdk">
  <PropertyGroup>
    <OutputType>Exe</OutputType>
    <TargetFramework>netcoreapp3.1</TargetFramework>
  </PropertyGroup>
  <ItemGroup>
    <FsYacc Include="Parser.fsy">
      <OtherFlags>--module Parser</OtherFlags>
    </FsYacc>
    <FsLex Include="Lexer.fsl">
      <OtherFlags>--module Lexer --unicode</OtherFlags>
    </FsLex>
    <Compile Include="Lexer.fs" />
    <Compile Include="Program.fs" />
  </ItemGroup>
  <ItemGroup>
    <PackageReference Include="FsLexYacc" Version="10.0.0" />
  </ItemGroup>
</Project>
            \end{minted}
        \end{scriptsize}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Собираем и добавляем Parser.fs}
        \framesubtitle{Тоже до лексера!}
        \begin{scriptsize}
            \begin{minted}{xml}
<Project Sdk="Microsoft.NET.Sdk">
  ...
  <ItemGroup>
    <FsYacc Include="Parser.fsy">
      <OtherFlags>--module Parser</OtherFlags>
    </FsYacc>
    <Compile Include="Parser.fs" />
    <FsLex Include="Lexer.fsl">
      <OtherFlags>--module Lexer --unicode</OtherFlags>
    </FsLex>
    <Compile Include="Lexer.fs" />
    <Compile Include="Program.fs" />
  </ItemGroup>
  ...
</Project>
            \end{minted}
        \end{scriptsize}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Правим лексер}
        \framesubtitle{Чтобы он генерил лексемы для парсера}
        \begin{small}
            \begin{minted}{fsharp}
{ 
open FSharp.Text.Lexing
open System
open Parser

let lexeme = LexBuffer<_>.LexemeString
}

let digit = ['0'-'9']

rule token = parse
| digit+  { INT(Int32.Parse(lexeme lexbuf)) }
            \end{minted}
        \end{small}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Пробуем, что получилось}
        \framesubtitle{В Program.fs}
        \begin{small}
            \begin{minted}{fsharp}
open FSharp.Text.Lexing

[<EntryPoint>]
let main argv =
    let testInput = "9"

    let lexbuf = LexBuffer<char>.FromString testInput
    let ast = Parser.start Lexer.token lexbuf
    printfn "%A" ast
    0 
            \end{minted}
        \end{small}
        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
9
            \end{minted}
        \end{exampleblock}
        Теперь наконец можно приступать к делу!
    \end{frame}

    \subsection{Грамматика}

    \begin{frame}[fragile]
        \frametitle{Вспомним грамматику}
        \begin{minted}{bnf}
E ::= E + E
    | E - E
    | E * E
    | E / E
    | -E
    | (E)
    | NUMBER

NUMBER ::= [0..9]+
        \end{minted}
    \end{frame}

    \subsection{AST}

    \begin{frame}[fragile]
        \frametitle{Позаимствуем из FParsec AST}
        \framesubtitle{В новый файл Ast.fs, до парсера}
        \begin{minted}{fsharp}
module Types

type Expression =
    | Plus of Expression * Expression
    | Minus of Expression * Expression
    | Multiplication of Expression * Expression
    | Division of Expression * Expression
    | Negation of Expression
    | Number of int
        \end{minted}
    \end{frame}

    \subsection{Токены}

    \begin{frame}[fragile]
        \frametitle{Требуемые токены}
        \framesubtitle{В парсере}
        \begin{minted}{text}
%{
open Types
%}

%token <int> INT
%token PLUS
%token MINUS
%token MUL
%token DIV
%token LPAR
%token RPAR
%token EOF

...
        \end{minted}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Поддержка в лексере}
        \begin{minted}{fsharp}
...

rule token = parse
| digit+  { INT(Int32.Parse(lexeme lexbuf)) }
| '+' { PLUS }
| '-' { MINUS }
| '*' { MUL }
| '/' { DIV }
| '(' { LPAR }
| ')' { RPAR }
| eof { EOF }
        \end{minted}
    \end{frame}

    \subsection{Парсер}

    \begin{frame}[fragile]
        \frametitle{Грамматика в парсере}
        \framesubtitle{Пока просто потестим, разбирается ли}
        \begin{minted}{text}
start: expression { 0 }

expression:
    | INT  {}
    | expression PLUS expression  {}
    | expression MINUS expression  {}
    | expression MUL expression  {}
    | expression DIV expression  {}
    | MINUS expression  {}
    | LPAR expression RPAR  {}
        \end{minted}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Попробуем}
        \begin{minted}{text}
open FSharp.Text.Lexing

[<EntryPoint>]
let main argv =
    let testInput = "1+2*3"

    let lexbuf = LexBuffer<char>.FromString testInput
    let ast = Parser.start Lexer.token lexbuf
    printfn "%A" ast
    0 
        \end{minted}
        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
0
            \end{minted}
        \end{exampleblock}
    \end{frame}

    \subsection{Пробелы}

    \begin{frame}[fragile]
        \frametitle{Пробелы?}
        \begin{minted}{fsharp}
open FSharp.Text.Lexing

[<EntryPoint>]
let main argv =
    let testInput = "1 + 2 * 3"

    let lexbuf = LexBuffer<char>.FromString testInput
    let ast = Parser.start Lexer.token lexbuf
    printfn "%A" ast
    0 
        \end{minted}
        \begin{exampleblock}{Вывод}
            \begin{minted}{text}
Unhandled exception. System.Exception: unrecognized input
            \end{minted}
        \end{exampleblock}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Чиним, в лексере}
        \begin{minted}{fsharp}
let digit = ['0'-'9']
let whitespace = [' ' '\t' '\r' '\n']

rule token = parse
| digit+  { INT(Int32.Parse(lexeme lexbuf)) }
| '+' { PLUS }
| '-' { MINUS }
| '*' { MUL }
| '/' { DIV }
| '(' { LPAR }
| ')' { RPAR }
| whitespace { token lexbuf }
| eof { EOF }
        \end{minted}
        Теперь всё работает
    \end{frame}

    \subsection{Построение AST}

    \begin{frame}[fragile]
        \frametitle{Семантические действия}
        \framesubtitle{Плюс выкинем start}
        \begin{minted}{text}
%start expression
%type <Expression> expression

%%

expression:
    | INT  { Number($1) }
    | expression PLUS expression  { Plus($1, $3) }
    | expression MINUS expression  { Minus($1, $3) }
    | expression MUL expression  { Multiplication($1, $3) }
    | expression DIV expression  { Division($1, $3) }
    | MINUS expression  { Negation($2) }
    | LPAR expression RPAR  { $2 }
        \end{minted}
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Запустим}
        \begin{minted}{fsharp}
open FSharp.Text.Lexing

[<EntryPoint>]
let main argv =
    let testInput = "1 + 2 * 3"

    let lexbuf = LexBuffer<char>.FromString testInput
    let ast = Parser.expression Lexer.token lexbuf
    printfn "%A" ast
    0 
        \end{minted}
        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
Plus (Number 1,Multiplication (Number 2,Number 3))
            \end{minted}
        \end{exampleblock}
        Магия LALR-разбора!
    \end{frame}

    \subsection{Приоритет операций}

    \begin{frame}[fragile]
        \frametitle{Приоритет операций?}
        \begin{minted}{fsharp}
[<EntryPoint>]
let main argv =
    let testInput = "1 * 2 + 3"

    let lexbuf = LexBuffer<char>.FromString testInput
    let ast = Parser.expression Lexer.token lexbuf
    printfn "%A" ast
    0 
        \end{minted}
        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
Multiplication (Number 1,Plus (Number 2,Number 3))
            \end{minted}
        \end{exampleblock}
        Не магия, а неоднозначность вывода :(
    \end{frame}

    \begin{frame}[fragile]
        \frametitle{Подправим грамматику}
        \framesubtitle{Прямо в парсере, благо это просто}
        \begin{small}
            \begin{minted}{text}
expression:
    | term PLUS term  { Plus($1, $3) }
    | term MINUS term { Minus($1, $3) }
    | term { $1 }

term:
    | factor MUL factor { Multiplication($1, $3) }
    | factor DIV factor  { Division($1, $3) }
    | factor { $1 }

factor:
    | INT  { Number($1) }
    | MINUS expression  { Negation($2) }
    | LPAR expression RPAR  { $2 }
            \end{minted}
        \end{small}
    \end{frame}

    \subsection{Что получилось}

    \begin{frame}[fragile]
        \frametitle{Что получилось}
        \begin{exampleblock}{Вывод}
            \begin{minted}{fsharp}
Plus (Multiplication (Number 1,Number 2),Number 3)
            \end{minted}
        \end{exampleblock}
        \begin{itemize}
            \item Полные исходники \url{https://github.com/yurii-litvinov/FsParsersDemo}
            \item Некоторое объяснение того, что это было:
            
                \url{https://github.com/fsprojects/FsLexYacc/blob/master/docs/content/jsonParserExample.md}
            \item Ещё пример: \url{https://en.wikibooks.org/wiki/F_Sharp_Programming/Lexing_and_Parsing}
            \item Внятное описание того, что происходит: D. Syme, A. Granicz, A. Cisternino, Expert F\#, 2007
            \begin{itemize}
                \item В более свежих изданиях про FsLex/FsYacc нет, такие дела
            \end{itemize}
            \item Жалкое подобие документации на домашней странице проекта: \url{https://fsprojects.github.io/FsLexYacc/}
        \end{itemize}
    \end{frame}

    \section{Заключение}

    \begin{frame}
        \frametitle{Выводы}
        \begin{itemize}
            \item FParsec --- встроенный DSL для описания грамматик
            \begin{itemize}
                \item Легко интегрируется с кодом
                \item Не надо отдельных файлов и странных упражнений с проектными файлами
                \item Нисходящий разбор, боль с грамматикой
                \item И боль с построением AST
            \end{itemize}
            \item FsLex/FsYacc --- внешний DSL
            \begin{itemize}
                \item Странный синтаксис (и, соответственно, не очень с поддержкой в редакторе)
                \item Нужны внешние инструменты
                \item Восходящий разбор, никакой боли с грамматикой
                \begin{itemize}
                    \item Нам просто повезло, у LR-разбора свои проблемы
                \end{itemize}
                \item Не очень с сообщениями об ошибках
            \end{itemize}
        \end{itemize}
    \end{frame}

\end{document}
