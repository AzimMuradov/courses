\documentclass[a5paper]{article}
\usepackage[a5paper, top=8mm, bottom=8mm, left=8mm, right=8mm]{geometry}

\usepackage{polyglossia}
\setdefaultlanguage[babelshorthands=true]{russian}

\usepackage{fontspec}
\setmainfont{FreeSerif}
\newfontfamily{\russianfonttt}[Scale=0.7]{DejaVuSansMono}

\usepackage[font=scriptsize]{caption}

\usepackage{amsmath}
\usepackage{amssymb,amsfonts,textcomp}
\usepackage{color}
\usepackage{array}
\usepackage{hhline}
\usepackage{cite}

\usepackage[hang,multiple]{footmisc}
\renewcommand{\footnotelayout}{\raggedright}

\PassOptionsToPackage{hyphens}{url}\usepackage[xetex,linktocpage=true,plainpages=false,pdfpagelabels=false]{hyperref}
\hypersetup{colorlinks=true, linkcolor=blue, citecolor=blue, filecolor=blue, urlcolor=blue, pdftitle=1, pdfauthor=, pdfsubject=, pdfkeywords=}

\usepackage{tabu}

\usepackage{graphicx}
\usepackage{indentfirst}
\usepackage{multirow}
\usepackage{subfig}
\usepackage{footnote}
\usepackage{minted}

\sloppy
\pagestyle{plain}

\title{Лекция 2: Продолжение про F\#}

\date{}

\begin{document}

\maketitle
\thispagestyle{empty}

\section{Юнит-тестирование в F\#}

Начнём с юнит-тестирования, штуки, хоть и не связанной с функциональным программированием напрямую, но имеющей в F\# несколько приятных функциональных особенностей. Кстати, теперь юнит-тесты в домашке обязательны.

Поскольку F\# компилируется в байт-коды .NET, то нет никаких причин не работать стандартным библиотекам модульного тестирования, которые работают и в C\#, например, NUnit, Microsoft Testing Framework и т.д. Ими вполне можно пользоваться, но есть более красивые с функциональной точки зрения обёртки над ними, которые позволят писать тесты в более F\#-овом стиле, прежде всего FsUnit, стандарт де-факто в модульном тестировании для F\#. Есть и библиотеки, специфичные для F\#: FsCheck и Unquote, например (на самом деле, не совсем для F\#, FsCheck портирована с Haskell, но в C\# точно нет ничего такого --- хотя никто не мешает тесты для программ на C\# писать на F\#-е, естественно, пользуясь его тулами).

Вот небольшой пример теста с FsUnit-ом:

\begin{minted}{fsharp}
module ``Project Euler - Problem 1`` =
    open NUnit.Framework
    open FsUnit

    let GetSumOfMultiplesOf3And5 max =
        seq{3 .. max - 1} 
        |> Seq.fold(fun acc number ->
                    (if (number % 3 = 0 || number % 5 = 0) then
                         acc + number else acc)) 0

    [<Test>]
    let ``Sum of multiples of 3 and 5 to 10 should return 23`` () =
        GetSumOfMultiplesOf3And5(10) |> should equal 23
\end{minted}

Тут показан, кстати, способ записи идентификаторов через двойные апострофы --- вся строка внутри считается именем, даже если там пробелы, ключевые слова и прочая жесть. Сам тест помечен атрибутом \textit{[<Test>]}, и вообще в F\# атрибуты пишутся в скобках вида [< >] (потому что просто квадратные скобки, как в C\#, заняты под списки). \textit{should equal} --- это что-то вроде \textit{Assert.Equals}, только круче.

Круче тем, что условие, которое надо проверить, на самом деле может быть довольно сложным, не просто равенство-истинность-не null, как принято в C\#. Условие записывается в виде так называемых \textit{матчеров}. Для тех, кто в курсе, такой же подход используется в библиотеке Hamcrest/NHamcrest. Вот примеры, из которых по идее всё должно стать понятно:

\begin{minted}{fsharp}
1 |> should equal 1
1 |> should not' (equal 2)
10.1 |> should (equalWithin 0.1) 10.11
"ships" |> should startWith "sh"
"ships" |> should not' (endWith "ss")
"ships" |> should haveSubstring "hip"
[1] |> should contain 1
[] |> should not' (contain 1)
anArray |> should haveLength 4

(fun () -> failwith "BOOM!" |> ignore) 
    |> should throw typeof<System.Exception>

shouldFail (fun () -> 5/0 |> ignore)
\end{minted}

Есть некоторые заморочки с should throw и shouldFail, они хотят функцию без аргументов, возвращающую ничего, поэтому при вызове настоящий код обёрнут в лямбду.

Вот ещё примеры матчеров:

\begin{minted}{fsharp}
true |> should be True
false |> should not' (be True)
"" |> should be EmptyString
null |> should be Null

anObj |> should not' (be sameAs otherObj)

11 |> should be (greaterThan 10)
10.0 |> should be (lessThanOrEqualTo 10.1)

0.0 |> should be ofExactType<float>
1 |> should not' (be ofExactType<obj>)

Choice<int, string>.Choice1Of2(42) |> should be (choice 1)

"test" |> should be instanceOfType<string>
"test" |> should not' (be instanceOfType<int>)

2.0 |> should not' (be NaN)

[1; 2; 3] |> should be unique

[1; 2; 3] |> should be ascending
[1; 3; 2] |> should not' (be ascending)
[3; 2; 1] |> should be descending
[3; 1; 2] |> should not' (be descending)
\end{minted}

Более интересная, хотя и реже используемая штука --- это FsCheck. FsCheck берёт функцию, рефлексией понимает, что функция принимает, и генерирует случайные значения её параметрам, после чего сколько-то раз её вызывает. Функция должна возвращать булевое значение. Если функция вернёт false, то FsCheck скажет, что тест провален, и ещё и покажет минимальный контрпример, на котором, собственно, получилось плохо. Например: 

\begin{minted}{fsharp}
open FsCheck

let revRevIsOrig (xs:list<int>) = List.rev(List.rev xs) = xs

Check.Quick revRevIsOrig
// Ok, passed 100 tests.

let revIsOrig (xs:list<int>) = List.rev xs = xs
Check.Quick revIsOrig
// Falsifiable, after 2 tests (2 shrinks) (StdGen (338235241,296278002)):
// Original:
// [3; 0]
// Shrunk:
// [1; 0]
\end{minted}

Замечу, что FsCheck прекрасно работает в паре с FsUnit (бросает исключение, которое FsUnit ловит и помечает тест как проваленный), так что писать часть тестов руками, а часть отдавать FsCheck не зазорно и повышает качество кода.

Ещё одна интересная библиотека --- это Unquote. Вообще, она просто интерпретатор F\#-а, использующий встроенный в язык механизм Quotation-ов, дающий возможность в программе получить дерево разбора куска кода на F\#, потом обходить его как обычное дерево и делать с ним что угодно. Unquote хороша тем, что умеет не просто сказать, прошёл тест или нет, но и если не прошёл, показать последовательность преобразований, которая привела к ошибке. Вот пример:

\begin{minted}{fsharp}
[<Test>]
let ``Unquote demo`` () =
    test <@ ([3; 2; 1; 0] |> List.map ((+) 1)) = [1 + 3..1 + 0] @>

// ([3; 2; 1; 0] |> List.map ((+) 1)) = [1 + 3..1 + 0]
// [4; 3; 2; 1] = [4..1]
// [4; 3; 2; 1] = []
// false
\end{minted}

Эти ``<@'' и ``@>'' и есть границы quotation-а, всё, что внутри, не вычисляется, а превращается в дерево разбора, которое отдаётся функции test из Unquote.

И конечно, библиотека для создания mock-объектов. В случае F\#-а это Foq, тоже использующая quotation-ы, но на сей раз для генерации моков. Вот пример:

\begin{minted}{fsharp}
[<Test>]
let ``Foq demo`` () =
    let mock = Mock<System.Collections.Generic.IList<int>>()
                .Setup(fun x -> <@ x.Contains(any()) @>).Returns(true)
                .Create()

    mock.Contains 1 |> Assert.True
\end{minted}

Этот мок имитирует мутабельный список, который на запрос Contains всегда возвращает true. Собственно, тоже хорошо работает с FsUnit и NUnit, можно использовать, чтобы красиво генерить тестовые заглушки и уменьшать зависимость теста от других кусков программы.


\section{Каррирование}

Для функционального программирования очень важно такое соображение, что функция от n аргументов может быть представлена как функция от одного аргумента, возвращающая функцию от n - 1 аргумента --- это называется \textbf{каррирование}. Каррирование поддержано и синтаксически (кстати, поэтому в списке аргументов функций в F\# нет никаких скобок), и системой типов, что позволяет не передавать один из аргументов в функцию и получать функцию, которая хочет ещё аргументов. Зачем --- частичное применение, например:

\begin{minted}{fsharp}
let shift (dx, dy) (px, py) = (px + dx, py + dy)
let shiftRight = shift (1, 0)
let shiftUp = shift (0, 1)
let shiftLeft = shift (-1, 0)
let shiftDown = shift (0, -1)
\end{minted}

Можно это повызывать из F\# Interactive:

\begin{minted}{text}
> shiftDown (1, 1);;
val it : int * int = (1, 0)
\end{minted}

Практически полезно это не только тем, чтобы производить впечатление на коллег, но и для функций высших порядков. Например, как посчитать количество элементов сразу у пяти списков:

\begin{minted}{fsharp}
let lists = [[1; 2]; [1]; [1; 2; 3]; [1; 2]; [1]]
let lengths = List.map List.length lists
\end{minted}

List.length тут функция, принимающая один аргумент, который ей передаёт List.map, когда идёт по списку. Посмотрим, что будет, если функция над списком принимает два аргумента, например, тот же map. Положим, мы хотим возвести элементы всех пяти списков в квадрат:

\begin{minted}{fsharp}
let lists = [[1; 2]; [1]; [1; 2; 3]; [1; 2]; [1]]
let squares = List.map (List.map (fun x -> x * x)) lists
\end{minted}

Тут уже каррирование работает во всей красе, внутренний List.map возвращает функцию от одного аргумента типа ``список'', которую внешний List.map применяет к каждому элементу списка списков. Обратите внимание, что большинство функций из стандартной библиотеки принимают список последним аргументом, как раз для того, чтобы каррировать было удобнее.

\subsection{Операторы конвейера и композиции}

Каррирование работает ещё лучше, если использовать оператор конвейера, или pipe forward:

\begin{minted}{fsharp}
let (|>) x f = f x
\end{minted}

Он просто принимает аргумент и функцию, и вызывает функцию, передавая ей аргумент. Зачем --- чтобы записывать вычисление в виде последовательно применяемых пребразований, вот так:

\begin{minted}{fsharp}
let sumFirst3 ls = ls |> Seq.take 3 |> Seq.fold (+) 0
\end{minted}

Согласитесь, это менее противоестественно, чем как бы это выглядело на C\# или Java:

\begin{minted}{fsharp}
let sumFirst3 ls= Seq.fold (+) 0 (Seq.take 3 ls)
\end{minted}

Есть ещё оператор композиции, который почти как pipe forward, но позволяет аргумент функции в явном виде не писать:

\begin{minted}{fsharp}
let (>>) f g x = g (f x)

let sumFirst3 = Seq.take 3 >> Seq.fold (+) 0
let result = sumFirst3 [1; 2; 3; 4; 5]
\end{minted}

Его можно понимать как математическую композицию --- принимает две функции и возвращает функцию, которая сначала применяет первую функцию, затем вторую. А можно понимать как штуку, которая собирает конвейер, по которому потом пойдут данные. Сравните: 

\begin{minted}{fsharp}
let sumFirst3 = fun ls -> ls |> Seq.take 3 |> Seq.fold (+) 0
let sumFirst3 = Seq.take 3 >> Seq.fold (+) 0
\end{minted}

Есть ещё обратный конвейер и обратная композиция:

\begin{minted}{fsharp}
let (<|) f x = f x
let (<<) f g x = f (g x)
\end{minted}

Зачем, ведь обратный конвейер вообще ничего не делает? На самом деле, чтобы воспользоваться приоритетом операций и не ставить скобки там, где иначе пришлось бы их ставить:

\begin{minted}{fsharp}
printfn "Result = %d" <| factorial 5
\end{minted}

Сравните с

\begin{minted}{fsharp}
printfn "Result = %d" (factorial 5)
\end{minted}

\section{Использование библиотек .NET}

Как уже не раз говорилось, классы из .NET работают в F\# ``даром'', кроме того, F\# имеет ряд синтаксических особенностей, которые делают работу даже более приятной, чем ожидалось. Пример, небольшой текстовый редактор на Windows Forms, который грузит HTML-документ с удалённого адреса:

\begin{minted}{fsharp}
open System.Windows.Forms

let form = new Form(Visible = false, TopMost = true, Text = "Welcome to F#")
let textB = new RichTextBox(Dock = DockStyle.Fill, Text = "Some text")
form.Controls.Add(textB)

open System.IO
open System.Net

/// Get the contents of the URL via a web request
let http (url: string) =
    let req = System.Net.WebRequest.Create(url)
    let resp = req.GetResponse()
    let stream = resp.GetResponseStream()
    let reader = new StreamReader(stream)
    let html = reader.ReadToEnd()
    resp.Close()
    html

textB.Text <- http("http://www.google.com")

form.ShowDialog() |> ignore
\end{minted}

\textit{open} --- это что-то вроде using, импорт имён из пространства имён. \textit{new Form} --- обычный вызов обычного конструктора, только в его параметры передаются не только параметры конструктора, но и значения public-свойств, которые мы хотим установить изначально. ``<-'' означает оператор присваивания (.NET-классы же мутабельны, их свойства можно менять, несмотря на то, что ссылка на объект не мутабельна). Функция ignore определена так:

\begin{minted}{fsharp}
let ignore x = ()
\end{minted}

Нужна она только для того, чтобы компилятор не выдавал предупреждение о неиспользованном значении, которое вернул метод \textit{form.ShowDialog}.

\section{Сопоставление шаблонов}

Тут уже несколько раз встречался оператор \textit{match}, настала пора рассказать про него поподробнее. Это почти как \textit{switch} в других языках, но немного круче, потому что позволяет тут же декомпозировать сложную структуру данных, сопоставляя переменную с шаблоном. Опять-таки, в C\# с версии 7.1 появилось что-то такое, но в силу ограниченности шаблонов в C\#, не такое выразительное. В F\# же можно писать, например, так:

\begin{minted}{fsharp}
let urlFilter url agent =
    match (url, agent) with
    | "http://www.google.com", 99 -> true
    | "http://www.yandex.ru" , _ -> false
    | _, 86 -> true
    | _ -> false
\end{minted}

Подчёркивание означает ``что угодно'', и обратите внимание на два последних случая, в первом из них подчёркивание относится к первому элементу пары, во втором --- ко всей паре сразу.

Или вот так (пример с одного из предыдущих слайдов):

\begin{minted}{fsharp}
let rec length l =
    match l with
    | [] -> 0
    | h :: t -> 1 + length t
\end{minted}

Тут мы смотрим, если список пустой, то его длина 0, если нет, то в нём есть голова - cons - хвост, и его длина --- это длина его хвоста плюс 1.

Есть ещё ключевое слово when, позволяющее указать в match произвольное булевое условие:

\begin{minted}{fsharp}
let sign x =
    match x with
    | _ when x < 0 -> -1
    | _ when x > 0 -> 1
    | _ -> 0
\end{minted}

Это довольно странно делать match-ем, но в каком-то смысле получилось красивее, чем последовательность if-ов.

Однако важно понимать, что F\# не пытается в match считать переменные с одинаковыми именами одной переменной (как делает, например, Prolog), потому что это привело бы к резкому увеличению трудоёмкости match-а (до экспоненциальной, как в Prolog). Например, вот так работать не будет:

\begin{minted}{fsharp}
let isSame pair =
    match pair with
    | (a, a) -> true
    | _ -> false
\end{minted}

А вот так будет:

\begin{minted}{fsharp}
let isSame pair =
    match pair with
    | (a, b) when a = b -> true
    | _ -> false
\end{minted}

Какие шаблоны ещё можно использовать в match (и, кстати, в let):

\begin{tabu} {| X[0.9 l p] | X[1 l p] | X[1 l p] |}
	\tabucline-
	Синтаксис                               & Описание                  & Пример                  \\
	\tabucline-
	\everyrow{\tabucline-}
	$(pat, \ldots, pat)$                    & Кортеж                    & $(1, 2, (``3``, x))$    \\
	$[pat; \ldots; pat]$                    & Список                    & $[x; y; 3]$             \\
	$pat :: pat$                            & cons                      & $h :: t$                \\
	$pat\ |\ pat$                           & "Или"                     & $[x]\ |\ ["X"\ ;\ x]$ \\
	$pat\ \&\ pat$                          & "И"                       & $[p] \& [(x, y)]$       \\
	$pat\ as\ id$                           & Именованный шаблон        & $[x]\ as\ inp$          \\
	$id$                                    & Переменная                & $x$                     \\
	$\_$                                    & Wildcard (что угодно)     & $\_$                    \\
	литерал                                 & Константа                 & $239, DayOfWeek.Monday$ \\
	$:?\ type$                              & Проверка на тип           & $:?\ string$            \\
\end{tabu}

\section{Последовательности}

Последовательности --- один из самых частоиспользуемых ленивых типов данных, в стандартной библиотеке он называется \textit{seq}, но на самом деле это не более чем синоним к \textit{IEnumerable<T>}. То есть последовательность --- это просто штука, по которой можно ходить итератором. Таким образом, на самом деле и строки, и списки, и массивы, и обычные дотнетовские списки, и практически всё остальное реализует \textit{seq}. А это значит, что все операции \textit{seq} применимы и к ним ко всем.

``Просто последовательность'' в F\# описывается так:

\begin{minted}{fsharp}
seq {0 .. 2}
seq {1I .. 1000000000000I}
\end{minted}

\textit{seq} --- это просто класс, возвращающий энумератор, поэтому она, собственно, и ленивая. Хранить миллиард чисел в памяти было бы очень грустно, поэтому их никто и не хранит, просто есть энумератор, который по текущему числу может вернуть следующее и знает, когда остановиться. Поэтому последовательности могут быть хоть бесконечными. Но зато чтобы вернуться к предыдущему элементу, последовательность надо перевычислять с самого начала.

Вот небольшой пример того, как с последовательностями работать:

\begin{minted}{fsharp}
open System.IO
let rec allFiles dir =
    Seq.append
    (dir |> Directory.GetFiles)
    (dir |> Directory.GetDirectories 
         |> Seq.map allFiles 
         |> Seq.concat)
\end{minted}

Тут мы обходим файловую систему, собирая список файлов в текущей папке и всех её подпапках. Обратите внимание на использование операций модуля \textit{Seq}, тут используется тот же паттерн, что и в списках --- структура данных, которая ничего не умеет, и статический класс, который умеет всё, но ничего не хранит. Вот какие ещё методы бывают:

\begin{center}
    \begin{tabu} {| X[0.5 l p] | X[1 l p] |}
        \tabucline-
        Операция                      & Тип                    \\
        \tabucline-
        \everyrow{\tabucline-}
        Seq.append                    & $\#seq<'a> \to \#seq<'a> \to seq<'a>$ \\
        Seq.concat                    & $\#seq<\#seq<'a>> \to seq<'a>$ \\
        Seq.choose                    & $('a \to 'b\ option) \to \#seq<'a> \to seq<'b>$ \\
        Seq.empty                     & $seq<'a>$ \\
        Seq.map                       & $('a \to 'b) \to \#seq<'a> \to \#seq<'b>$ \\
        Seq.filter                    & $('a \to bool) \to \#seq<'a> \to seq<'a>$ \\
        Seq.fold                      & $('s \to 'a \to 's) \to 's \to seq<'a> \to 's$ \\
        Seq.initInfinite              & $(int \to 'a) \to seq<'a>$ \\
    \end{tabu}
\end{center}

Как обычно, тут приводится только небольшая часть методов, наиболее интересных по тем или иным причинам. Традиционные map-filter-fold, два сорта операций конкатенации (append принимает два аргумента, concat --- последовательность из последовательностей, которые надо склеить), choose --- это map + filter. empty --- это просто пустая последовательность, initInfinite принимает функцию-генератор, которая по данному индексу должна вернуть элемент, и возвращает бесконечную последовательность. Есть ещё Seq.unfold, которая тоже так умеет, но несколько более умно (она как fold, только наоборот, что, впрочем, понятно по её названию).

Кстати, решётки перед типами параметров означают ``тип и все, кто от него наследуется''. Например, append может принять список и массив, но возвращает он именно последовательность.

Для последовательностей работает тот же синтаксис генерации, что и для списков, так что можно писать вот так:

\begin{minted}{fsharp}
let squares = seq { for i in 0 .. 10 -> (i, i * i) }
seq { for (i, isquared) in squares -> 
         (i, isquared, i * isquared) }
\end{minted}

Стрелочку надо читать как yield и обозначает она то же, что yield return в C\# --- когда дошли до этого места, вернуть значение, а потом, когда нас попросят следующее, продолжить с этого же места и считать, будто мы не прерывались. Вот ещё пример, генерим координаты белых (или чёрных) клеток шахматной доски:

\begin{minted}{fsharp}
let checkerboardCoordinates n =
    seq { for row in 1 .. n do
        for col in 1 .. n do
            if (row + col) % 2 = 0 then
                yield (row, col) }
\end{minted}

Можно переписать через yield предыдущий пример с обходом файловой системы:

\begin{minted}{fsharp}
let rec allFiles dir =
    seq { for file in Directory.GetFiles(dir) -> file
        for subdir in Directory.GetDirectories dir ->> 
            (allFiles subdir) }
\end{minted}

Здесь используется новая конструкция --- ``->>'' или \textit{yield!} (читается как yield-bang) --- если yield добавляет к результату один элемент, то yield! добавляет все элементы последовательности, один за одним (то есть это равносильно циклу for с yield-ом).

А вот как можно получить последовательность всех строчек в файле:

\begin{minted}{fsharp}
let reader =
    seq { 
        use reader = new StreamReader(
            File.OpenRead("test.txt")
            )
        while not reader.EndOfStream do
            yield reader.ReadLine() }
\end{minted}

Обратите внимание на use, это как using в C\# --- объявить переменную типа, реализующего интерфейс IDisposable, и вызвать его метод Dispose как только переменная выйдет из области видимости. Это очень  похоже на идиому RAII из C++ и служит тем же целям --- корректно завершить работу с ресурсом даже с учётом бросания исключений.

На самом деле, так читать из файла --- плохая идея, потому что последовательности ленивые, следовательно, следующая строка будет считана только когда её попросят. При этом файл будет открыт, пока программа не дойдёт до самого конца файла. А вот если в конце сделать Seq.toList, это заставит файл считаться целиком, списки не ленивы и держат всё своё содержимое в памяти.

\section{Записи}

В F\# есть аналог структур из C\#, только тут они называются ``записи'' и немутабельны по умолчанию. И в отличие от C\#, они по умолчанию ссылочные типы. Описание типа-записи выглядит вот так:

\begin{minted}{fsharp}
type Person =
    { Name: string;
      DateOfBirth: System.DateTime; }
\end{minted}

Объект этого типа описывается, например, так:

\begin{minted}{fsharp}
{ Name = "Bill"; 
  DateOfBirth = new System.DateTime(1962, 09, 02) }
\end{minted}

Или так:

\begin{minted}{fsharp}
{ new Person
  with Name = "Anna"
  and DateOfBirth = new System.DateTime(1968, 07, 23) }
\end{minted}

Обратите внимание, что в первом случае тип записи явно не указывается, выводилка типов находит тип с подходящими полями и считает типом записи его.

Поскольку записи немутабельны, традиционный принцип работы ``скопировать и поменять'' без специальной поддержки со стороны языка был бы очень неудобен, пришлось бы вручную копировать кучу полей. Но синтаксис копирования с изменением есть, выглядит так:

\begin{minted}{fsharp}
type Car =
    {
        Make : string
        Model : string
        Year : int
    }

let thisYear's = { Make = "SomeCar"; 
                   Model = "Luxury Sedan"; 
                   Year = 2010 }
let nextYear's = { thisYear's with Year = 2011 }
\end{minted}

Кстати, обратите внимание, что апостроф (одиночная кавычка) --- вполне законная часть имени, может стоять на любой позиции, кроме первой.

\section{Размеченные объединения}

Размеченные объединения --- это типичный для функциональных языков тип, который почему-то редко встречается в объектно-ориентированных языках. Ближе всего размеченное объединение к конструкции union в C++ или к вариантным записям Паскаля. Это тип, значение которого может быть либо чем-то, либо чем-то ещё (одного из нескольких ``подтипов''). Например:

\begin{minted}{fsharp}
type Route = int
type Make = string
type Model = string

type Transport =
    | Car of Make * Model
    | Bicycle
    | Bus of Route

let bus = Bus(420)
\end{minted}

Переменная bus имеет тип Transport, при этом является альтернативой Bus и хранит в себе данные типа Route (который на самом деле синоним int). А можно было бы написать

\begin{minted}{fsharp}
let car = Car("SomeCar", "Luxury Sedan")
\end{minted}

Тогда car тоже была бы типа Transport, но тем не менее, хранила бы в себе другие данные и помнила, что она машина. Кстати, эти Car, Bicycle и Bus часто называют дискриминаторами.

Некоторые типы данных, которые мы видели --- это на самом деле размеченные объединения. Например, option --- это либо None, либо Some что-то:

\begin{minted}{fsharp}
type 'a option =
    | None
    | Some of 'a
\end{minted}

Или даже список --- это либо пустой список, либо cons, содержащий в себе элемент и хвост списка. Да, размеченные объединения могут быть рекурсивны:

\begin{minted}{fsharp}
type 'a list =
    | ([])
    | (::) of 'a * 'a list
\end{minted}

Вот так выглядит создание объектов размеченных объединений:

\begin{minted}{fsharp}
type IntOrBool = I of int | B of bool
let i = I 99
let b = B true

type C = Circle of int | Rectangle of int * int

[1..10]
|> List.map Circle

[1..10]
|> List.zip [21..30]
|> List.map Rectangle
\end{minted}

Видно, что дискриминатор, используемый для создания объекта размеченного объединения, ведёт себя как функция.

Размеченные объединения можно разбирать с помощью шаблонов, удобнее всего это делать в match-е, например, так:

\begin{minted}{fsharp}
type Tree<'a> =
    | Tree of 'a * Tree<'a> * Tree<'a>
    | Tip of 'a

let rec size tree =
    match tree with
    | Tree(_, l, r) -> 1 + size l + size r
    | Tip _ -> 1
\end{minted}

Поэтому, кстати, размеченные объединения хороши для представления неоднородных иерархических данных, например, деревьев разбора. Поэтому F\# и OCaml так любят компиляторщики. Вот пример, дерево разбора логического выражения и код, который его вычисляет:

\begin{minted}{fsharp}
type Proposition =
    | True
    | And of Proposition * Proposition
    | Or of Proposition * Proposition
    | Not of Proposition

let rec eval (p: Proposition) =
    match p with
    | True -> true
    | And(p1, p2) -> eval p1 && eval p2
    | Or (p1, p2) -> eval p1 || eval p2
    | Not(p1) -> not (eval p1)

printfn "%A" <| eval (Or(True, And(True, Not True)))
\end{minted}

Кстати, обратите внимание, что кортеж в записи типа обозначается ``*'', а размеченное объединение --- ``|''. Это неспроста, кортеж и запись (которая по сути тоже кортеж, но с именованными полями) --- это декартово произведение множества значений входящих в них типов, а размеченное объединение --- это просто объединение множеств значений (например, размеченное объединение int и bool может принимать в качестве значений целые числа и true с false), отсюда использование чего-то, похожего на оператор ``или'' в записи. Кортеж --- это произведение типов, а размеченное объединение --- сумма типов.

Впрочем, кортежи хорошо работают вместе с размеченными объединениями, возможны взаимно ссылающиеся друг на друга типы, например, вот такое описание графа:

\begin{minted}{fsharp}
type node =
    { Name : string;
      Links : link list }
and link =
    | Dangling
    | Link of node
\end{minted}

\section{Паттерны функционального программирования}

Далее речь пойдёт о некоторых базовых приёмах, типичных для функциональных программ, которые чем-то похожи на низкоуровневые паттерны ООП. Вообще, в функциональном программировании паттерны не так распространены, как в объектно-ориентированном, возможно потому, что в функциональных программах такие конструкции выражаются гораздо естественнее. Тем не менее, есть некоторые общеизвестные приёмы, которые мы тут обсудим.

Во-первых, как сделать цикл, если использовать циклы нельзя --- заменить на рекурсию, передавая счётчик цикла и текущее состояние вычисления как параметр в рекурсивный вызов. Рассмотрим нерекурсивную программу, например, разложение на множители:

\begin{minted}{fsharp}
let factorizeImperative n =
    let mutable primefactor1 = 1
    let mutable primefactor2 = n
    let mutable i = 2
    let mutable fin = false
    while (i < n && not fin) do
        if (n % i = 0) then
            primefactor1 <- i
            primefactor2 <- n / i
            fin <- true
        i <- i + 1
    if (primefactor1 = 1) then None
    else Some (primefactor1, primefactor2)
\end{minted}

Кстати, это вполне валидная программа на F\#, но выглядит она так, будто написана на C\#-е. mutable --- это прямая противоположность const или readonly в C\#, разрешает переменной менять значение, ``<-'' --- оператор присваивания.

Как бы это можно было переписать рекурсивно:

\begin{minted}{fsharp}
let factorizeRecursive n =
    let rec find i =
        if i >= n then None
        elif (n % i = 0) then Some(i, n / i)
        else find (i + 1)
    find 2
\end{minted}

Здесь как раз и используется типичный для ФП приём --- завести вложенную функцию, которая бы принимала ещё один параметр (n попадает ей в замыкание, так что тоже доступен), и в этот параметр и передавать счётчик цикла. А дальше всё как обычно. В более сложных случаях состояние цикла может быть не только счётчиком, но и чем угодно, хоть здоровенной структурой.

Однако не всё так просто. Рассмотрим задачу создания списка чисел от 0 до 100000, и её очевидное императивное решение:

\begin{minted}{fsharp}
open System.Collections.Generic

let createMutableList () =
    let l = new List<int>()
    for i = 0 to 100000 do
        l.Add(i)
    l
\end{minted}

Воспользуемся приобретёнными знаниями и перепишем её рекурсивно:

\begin{minted}{fsharp}
let createImmutableList () =
    let rec createList i max =
        if i = max then
            []
        else
            i :: createList (i + 1) max
    createList 0 100000
\end{minted}

Запустим и с удивлением обнаружим, что она упала с переполнением стека. В общем-то, ничего удивительного, у нас 100000 рекурсивных вызовов тут, но если у нас в языке есть только рекурсия, то, кажется, у нас проблемы --- списки больше примерно 5000 элементов оказываются для нас вообще недоступны.

Разумеется, это не так. Посмотрим на проблему более внимательно, на примере типичной реализации факториала:

\begin{minted}{fsharp}
let rec factorial x =
    if x <= 1
    then 1 
    else x * factorial (x - 1)
\end{minted}

Это на самом деле то же самое, что и 

\begin{minted}{fsharp}
let rec factorial x =
    if x <= 1
    then
        1
    else
        let resultOfRecusion = factorial (x - 1)
        let result = x * resultOfRecusion
        result
\end{minted}

--- после рекурсивного вызова его результат ещё и умножается на значение x, которое, как параметр, лежит на стеке вызовов.

А если переписать факториал вот так:

\begin{minted}{fsharp}
let factorial x =
    let rec tailRecursiveFactorial x acc =
        if x <= 1 then
            acc
        else
            tailRecursiveFactorial (x - 1) (acc * x)
    tailRecursiveFactorial x 1
\end{minted}

Теперь умножение выполняется ДО рекурсивного вызова, при вычислении аргумента, и рекурсивный вызов --- это последний оператор функции. Тогда на самом деле кадр стека не нужен, потому что после рекурсивного вызова ничего уже не происходит, поэтому хранить параметры и локальные переменные не надо, да и адрес возврата нам неинтересен. Достаточно просто передать управление на начало функции, поменяв её параметры. Собственно, компилятор F\# (как и любого другого нормального функционального языка) так и делает, вот что получится при декомпиляции написанного выше кода в C\#:

\begin{minted}{csharp}
public static int tailRecursiveFactorial(int x, int acc)
{
    while (true)
    {
        if (x <= 1)
        {
            return acc;
        }
        acc *= x;
        x--;
    }
}
\end{minted}

Видим, что рекурсия заменилась просто циклом. Это возможно только тогда, когда рекурсивный вызов строго последнее действие, совершаемое функцией. Проблема F\# в том, что компилятор не подсказывает, что получилась или не получилась хвостовая рекурсия, поэтому надо самим аккуратно следить. Собственно, вот создавалка списка из предыдущего примера, переписанная с помощью хвостовой рекурсии, она уже реально работает:

\begin{minted}{fsharp}
let createImmutableList () =
    let rec createList i max result =
        if i = max then
            List.rev result
        else
            createList (i + 1) max (i :: result)
    createList 0 100000 []
\end{minted}


Чтобы аккуратно следить было не слишком больно, придумали паттерн ``аккумулятор'', который как раз и заключается в том, чтобы накапливать результат в одном из параметров, а потом вернуть результат целиком. Примеры выше были на самом деле примерами применения этого паттерна, вот ещё один пример, функция map:

\begin{minted}{fsharp}
let rec map f list =
    match list with
    | [] -> []
    | hd :: tl -> (f hd) :: (map f tl)
\end{minted}

--- так плохо, 

\begin{minted}{fsharp}
let map f list =
    let rec mapTR f list acc =
        match list with
        | [] -> acc
        | hd :: tl -> mapTR f tl (f hd :: acc)
    mapTR f (List.rev list) []
\end{minted}

--- так хорошо. Идея, как видим, тут точно такая же, как и в предыдущих примерах. Код получается страшнее, требуется вложенная функция, чтобы не показывать параметр-аккумулятор пользователю, зато рекурсивная программа работает так же, как обычная императивная программа с циклами.

Можно пойти дальше и вспомнить, что у нас язык-то функциональный, поэтому в качестве аккумулятора может выступать функция, которая постепенно строится, чтобы в конце рекурсии посчитаться и вернуть значение (или сделать то, что нужно). Например, распечатать список в обратном порядке:

\begin{minted}{fsharp}
let printListRev list =
    let rec printListRevTR list cont =
        match list with
        | [] -> cont ()
        | hd :: tl ->
            printListRevTR tl (fun () -> 
                printf "%d " hd; cont () )
    printListRevTR list (fun () -> printfn "Done!")
\end{minted}

Здесь при первом вызове printListRevTR передаётся весь список и функция, печатающая ``Done!'', затем происходит рекурсивный вызов, где в printListRevTR передаётся хвост списка и функция, печатающая голову, затем вызывающая функцию, печатающую ``Done!'', и т.д. В самом конце это всё вызывается, печатается последний элемент, предпоследний, ..., первый и ``Done!''.

Обратите внимание, что вызов функции-продолжения стоит последним, поэтому это тоже хвостовая рекурсия, кадра стека для каждого вызова не создаётся (хоть это и, вообще говоря, разные функции). Компилятор раскрывает их в последовательное выполнение.

Собственно, стиль, при котором в функцию передаётся функция, которая должна быть выполнена после того, как первая функция закончит работу, называется Continuation Passing Style, а сама передаваемая функция --- Continuation, продолжение. Стиль прижился в асинхронном программировании, где встречается даже в программах на C\# или Java --- например, в функцию, выполняющую запрос к серверу, передаются функции, которые вызываются, если от сервера пришёл ответ или произошла сетевая ошибка.

Несколько хуже обстоят дела с обходом ветвящихся структур данных, например, двоичного дерева. При использовании обычного паттерна ``аккумулятор'' нам бы пришлось в конце делать два рекурсивных вызова, для обхода левого и правого поддерева. Тогда они не могли бы быть хвостовой рекурсией, хотя бы один честно нуждался бы в кадре стека для хранения адреса возврата. Поэтому в таких случаях используют Continuation Passing Style, формируя функцию, которая последовательно вызовет себя для всех узлов дерева и при этом будет хвосторекурсивной. Вот пример:

\begin{minted}{fsharp}
type Tree<'a> =
    | Node of 'a * Tree<'a> * Tree<'a>
    | Empty

type ContinuationStep<'a> =
    | Finished
    | Step of 'a * (unit -> ContinuationStep<'a>)

let rec linearize binTree cont =
    match binTree with
    | Empty -> cont()
    | Node(x, l, r) ->
        Step(x, (fun () -> linearize l (fun () -> 
                           linearize r cont)))

let iter f binTree =
    let steps = linearize binTree (fun () -> Finished)

    let rec processSteps step =
        match step with
        | Finished -> ()
        | Step(x, getNext) -> 
            f x
            processSteps (getNext())
    
    processSteps steps
\end{minted}

Размеченное объединение ContinuationStep служит тут для хранения состояния вычисления --- оно может быть либо закончено, либо посещением узла с необходимостью вызвать функцию-продолжение (функцию типа unit -> ContinuationStep<'a>). Обход дерева выполняет функция linearize, которое, если дошла до листа, вызывает продолжение, а если посещает узел, возвращает Step со значением в этом узле и продолжением, которое вызовет linearize для левого поддерева, сказав ему (вот где хитрость), что потом в качестве продолжения надо вызвать linearize для правого поддерева. ну а потом функция iter получает последовательность step-ов и для каждого step-а вызывает функцию f и продолжение, чтобы получить следующий step и обработать его. То есть обход дерева не только хвосторекурсивный, но ещё и ленивый, linearize вызывается только когда надо получить следующий step.

\end{document}
