\documentclass[xetex,mathserif,serif]{beamer}
\usepackage{polyglossia}
\setdefaultlanguage[babelshorthands=true]{russian}
\usepackage{minted}
\usepackage{tabu}

\useoutertheme{infolines}

\usepackage{fontspec}
\setmainfont{FreeSans}
\newfontfamily{\russianfonttt}{FreeSans}

\definecolor{links}{HTML}{2A1B81}
\hypersetup{colorlinks,linkcolor=,urlcolor=links}

\tabulinesep=0.8mm

\title{Продолжение про F\#}
\author{Юрий Литвинов}
\date{01.03.2018г}

\begin{document}
	
	\frame{\titlepage}

	\section{Комментарии по домашке}

	\begin{frame}[fragile]
		\frametitle{Комментарии по домашке}
		\begin{itemize}
			\item Обработка ошибочного пользовательского ввода
			\item Обработка всех возможных входных данных
			\item Если элемента в списке не нашлось, надо возвращать не -1, а None
			\begin{itemize}
				\item Некорректное состояние должно быть невыразимо
			\end{itemize}
			\item Стараться не делать лишних вычислений, язык не ленивый
			\item Как порезать список на два:
				\begin{minted}{fsharp}
let rec split ls left right =
    match ls with
    | [] -> (left, right)
    | [a] -> (a::left, right)
    | a::b::tail -> split tail (a::left) (b::right)
				\end{minted}
		\end{itemize}
	\end{frame}

	\section{Последовательности}
	
	\begin{frame}[fragile]
		\frametitle{Последовательности}
		\framesubtitle{Ленивый тип данных}
		\begin{minted}{fsharp}
seq {0 .. 2}
seq {1I .. 1000000000000I}
		\end{minted}

		\begin{minted}{fsharp}
open System.IO
let rec allFiles dir =
    Seq.append
    (dir |> Directory.GetFiles)
    (dir |> Directory.GetDirectories 
         |> Seq.map allFiles 
         |> Seq.concat)
		\end{minted}
	\end{frame}

	\begin{frame}
		\frametitle{Типичные операции с последовательностями}
		\begin{small}
			\begin{tabu} {| X[0.5 l p] | X[1 l p] |}
				\tabucline-
				Операция                               & Тип                    \\
				\tabucline-
				\everyrow{\tabucline-}
				Seq.append                    & $\#seq<'a> \to \#seq<'a> \to seq<'a>$ \\
				Seq.concat                    & $\#seq<\#seq<'a>> \to seq<'a>$ \\
				Seq.choose                    & $('a \to 'b\ option) \to \#seq<'a> \to seq<'b>$ \\
				Seq.empty                     & $seq<'a>$ \\
				Seq.map                       & $('a \to 'b) \to \#seq<'a> \to \#seq<'b>$ \\
				Seq.filter                    & $('a \to bool) \to \#seq<'a> \to seq<'a>$ \\
				Seq.fold                      & $('s \to 'a \to 's) \to 's \to seq<'a> \to 's$ \\
				Seq.initInfinite              & $(int \to 'a) \to seq<'a>$ \\
			\end{tabu}
		\end{small}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Задание последовательностей}
		\begin{minted}{fsharp}
let squares = seq { for i in 0 .. 10 -> (i, i * i) }
seq { for (i, isquared) in squares -> 
         (i, isquared, i * isquared) }
		\end{minted}

		\begin{minted}{fsharp}
let checkerboardCoordinates n =
    seq { for row in 1 .. n do
        for col in 1 .. n do
            if (row + col) % 2 = 0 then
                yield (row, col) }
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Обход папок через yield}
		\begin{minted}{fsharp}
let rec allFiles dir =
    seq { for file in Directory.GetFiles(dir) -> file
        for subdir in Directory.GetDirectories dir ->> 
            (allFiles subdir) }
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Ленивое чтение из файла}
		\begin{minted}{fsharp}
let reader =
    seq { 
        use reader = new StreamReader(
            File.OpenRead("test.txt")
            )
        while not reader.EndOfStream do
            yield reader.ReadLine() }
		\end{minted}
	\end{frame}

	\section{Записи}
	
	\begin{frame}[fragile]
		\frametitle{Записи}
		\begin{minted}{fsharp}
type Person =
    { Name: string;
      DateOfBirth: System.DateTime; }
		\end{minted}

		\begin{minted}{fsharp}
{ Name = "Bill"; 
  DateOfBirth = new System.DateTime(1962, 09, 02) }

{ new Person
  with Name = "Anna"
  and DateOfBirth = new System.DateTime(1968, 07, 23) }
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Деконструкция}
		\begin{minted}{fsharp}
let person = { Name = "Anna"; 
               DateOfBirth = new System.DateTime(1968, 07, 23) }

let { Name = name; DateOfBirth = date} = person
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Клонирование записей}
		\begin{minted}{fsharp}
type Car =
    {
        Make : string
        Model : string
        Year : int
    }
		\end{minted}
		
		\begin{minted}{fsharp}
let thisYear's = { Make = "SomeCar"; 
                   Model = "Luxury Sedan"; 
                   Year = 2010 }
let nextYear's = { thisYear's with Year = 2011 }
		\end{minted}
	\end{frame}

	\section{Размеченные объединения}
	
	\begin{frame}[fragile]
		\frametitle{Размеченные объединения}
		\framesubtitle{Discriminated unions}
		\begin{minted}{fsharp}
type Route = int
type Make = string
type Model = string

type Transport =
    | Car of Make * Model
    | Bicycle
    | Bus of Route

let bus = Bus(420)
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Известные примеры}
		\begin{minted}{fsharp}
type 'a option =
    | None
    | Some of 'a
		\end{minted}

		\vspace{5mm}
		\begin{minted}{fsharp}
type 'a list =
    | ([])
    | (::) of 'a * 'a list
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Использование размеченных объединений}
		\begin{minted}{fsharp}
type IntOrBool = I of int | B of bool
let i = I 99
let b = B true
		\end{minted}
		
		\begin{minted}{fsharp}
type C = Circle of int | Rectangle of int * int

[1..10]
|> List.map Circle

[1..10]
|> List.zip [21..30]
|> List.map Rectangle
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Использование в match}
		\begin{minted}{fsharp}
type Tree<'a> =
    | Tree of 'a * Tree<'a> * Tree<'a>
    | Tip of 'a

let rec size tree =
    match tree with
    | Tree(_, l, r) -> 1 + size l + size r
    | Tip _ -> 1
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Пример}
		\framesubtitle{Дерево разбора логического выражения}
		\begin{minted}{fsharp}
type Proposition =
    | True
    | And of Proposition * Proposition
    | Or of Proposition * Proposition
    | Not of Proposition

let rec eval (p: Proposition) =
    match p with
    | True -> true
    | And(p1, p2) -> eval p1 && eval p2
    | Or (p1, p2) -> eval p1 || eval p2
    | Not(p1) -> not (eval p1)

printfn "%A" <| eval (Or(True, And(True, Not True)))
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Взаимосвязанные типы}
		\begin{minted}{fsharp}
type Node =
    { Name : string;
      Links : Link list }
and Link =
    | Dangling
    | Link of Node
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Одноэлементные объединения, без}
		\begin{minted}{fsharp}
type CustomerId = int  // синоним типа
type OrderId = int  // ещё один синоним типа

let printOrderId (orderId: OrderId) =
  printfn "The orderId is %i" orderId

let customerId = 1  
printOrderId customerId  // Печаааль
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Одноэлементные объединения, с}
		\begin{minted}{fsharp}
type CustomerId = CustomerId of int  // размеченное объединение 
type OrderId = OrderId of int // ещё одно

let printOrderId (OrderId orderId) = // деконструкция в параметре
    printfn "The orderId is %i" orderId

let customerId = CustomerId 1
printOrderId customerId  // Ошибка компиляции
		\end{minted}
	\end{frame}

	\section{Паттерны ФП}

	\begin{frame}[fragile]
		\frametitle{Замена цикла рекурсией}
		\framesubtitle{Императивное разложение на множители}
		\begin{minted}{fsharp}
let factorizeImperative n =
    let mutable primefactor1 = 1
    let mutable primefactor2 = n
    let mutable i = 2
    let mutable fin = false
    while (i < n && not fin) do
        if (n % i = 0) then
            primefactor1 <- i
            primefactor2 <- n / i
            fin <- true
        i <- i + 1
    if (primefactor1 = 1) then None
    else Some (primefactor1, primefactor2)
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Замена цикла рекурсией}
		\framesubtitle{Рекурсивное разложение на множители}
		\begin{minted}{fsharp}
let factorizeRecursive n =
    let rec find i =
        if i >= n then None
        elif (n % i = 0) then Some(i, n / i)
        else find (i + 1)
    find 2
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Хвостовая рекурсия, проблема}
		\framesubtitle{Императивный вариант}
		\begin{minted}{fsharp}
open System.Collections.Generic

let createMutableList () =
    let l = new List<int>()
    for i = 0 to 100000 do
        l.Add(i)
    l
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Хвостовая рекурсия, проблема}
		\framesubtitle{Рекурсивный вариант, казалось бы}
		\begin{minted}{fsharp}
let createImmutableList () =
    let rec createList i max =
        if i = max then
            []
        else
            i :: createList (i + 1) max
    createList 0 100000
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Факториал без хвостовой рекурсии}
		\begin{minted}{fsharp}
let rec factorial x =
    if x <= 1
    then 1 
    else x * factorial (x - 1)
		\end{minted}

		\begin{minted}{fsharp}
let rec factorial x =
    if x <= 1
    then
        1
    else
        let resultOfRecusion = factorial (x - 1)
        let result = x * resultOfRecusion
        result
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Факториал с хвостовой рекурсией}
		\begin{minted}{fsharp}
let factorial x =
    let rec tailRecursiveFactorial x acc =
        if x <= 1 then
            acc
        else
            tailRecursiveFactorial (x - 1) (acc * x)
    tailRecursiveFactorial x 1
		\end{minted}
	\end{frame}
	
	\begin{frame}[fragile]
		\frametitle{После декомпиляции в C\#}
		\begin{alertblock}{C\#}
			\begin{minted}{csharp}
public static int tailRecursiveFactorial(int x, int acc)
{
    while (true)
    {
        if (x <= 1)
        {
            return acc;
        }
        acc *= x;
        x--;
    }
}
			\end{minted}
		\end{alertblock}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Паттерн ``Аккумулятор''}
		\begin{minted}{fsharp}
let rec map f list =
    match list with
    | [] -> []
    | hd :: tl -> (f hd) :: (map f tl)

let map f list =
    let rec mapTR f list acc =
        match list with
        | [] -> acc
        | hd :: tl -> mapTR f tl (f hd :: acc)
    mapTR f (List.rev list) []
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Continuation Passing Style}
		\framesubtitle{Аккумулятор --- функция}
		\begin{minted}{fsharp}
let printListRev list =
    let rec printListRevTR list cont =
        match list with
        | [] -> cont ()
        | hd :: tl ->
            printListRevTR tl (fun () -> 
                printf "%d " hd; cont () )
    printListRevTR list (fun () -> printfn "Done!")
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Обход дерева}
		\frametitle{Когда всё не так просто}
		\begin{minted}{fsharp}
type ContinuationStep<'a> =
    | Finished
    | Step of 'a * (unit -> ContinuationStep<'a>)

let rec linearize binTree cont =
    match binTree with
    | Empty -> cont()
    | Node(x, l, r) ->
        Step(x, (fun () -> linearize l (fun () -> 
                           linearize r cont)))
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Обход дерева}
		\frametitle{Собственно, обход}
		\begin{minted}{fsharp}
let iter f binTree =
    let steps = linearize binTree (fun () -> Finished)

    let rec processSteps step =
        match step with
        | Finished -> ()
        | Step(x, getNext) -> 
            f x
            processSteps (getNext())
    
    processSteps steps
		\end{minted}
	\end{frame}

	\section{Продвинутый CI}

	\begin{frame}
		\frametitle{Непрерывная интеграция, для тех, кто всё пропустил}
		Зачем?
		\begin{itemize}
			\item Чтобы собиралось и работало не только у вас на компе
			\item Чтобы было эталонное окружение и эталонный процесс сборки, который можно повторить кому угодно
			\item Чтобы узнавать об ошибках сразу после коммита
		\end{itemize}
		Как?
		\begin{itemize}
			\item Облачные CI-системы: AppVeyor, Travis, CircleCI, ...
			\item Локальные CI-системы: Jenkins, TeamCity, TFS, ...
		\end{itemize}
	\end{frame}

	\begin{frame}
		\frametitle{Более подробно, как}
		\framesubtitle{AppVeyor}
		\begin{itemize}
			\item Логинимся на \url{https://www.appveyor.com/} по своему аккаунту на GitHub
			\item Добавляем свой репозиторий через New Project -> GitHub -> Add
			\item Добавляем в рабочую копию (прямо у себя на компе) appveyor.yml
			\begin{itemize}
				\item Файл должен называться именно так, вплоть до регистра букв
				\item Обычно его кладут в мастер, потом вмердживают в каждую ветку с домашкой
			\end{itemize}
			\item Пишем туда описание процесса сборки
			\item Коммитим-пушим
			\item Наслаждаемся билдом
		\end{itemize}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Что писать}
		\framesubtitle{AppVeyor, .NET Core}
		\url{https://www.appveyor.com/docs/appveyor-yml/}
		\begin{minted}{yaml}
image: Visual Studio 2017 

before_build: 
    - nuget restore myCoolHomework/Homework.sln

build: 
    project: myCoolHomework/Homework.sln

test_script: 
    - dotnet test myCoolHomework/Homework.sln
		\end{minted}
		Отступы и минусы критически важны.
	\end{frame}

	\begin{frame}
		\frametitle{При этом}
		\begin{itemize}
			\item Нужно добавить Reference на
			\begin{itemize}
				\item Microsoft.NET.Test.Sdk
				\item NUnit3TestAdapter
				\item NUnit
				\item FsUnit
			\end{itemize}
			\item Или другие библиотеки, которыми пользуетесь, но не забыть SDK и раннер.
			\item Как вообще создать проект:
			\begin{itemize}
				\item \url{https://docs.microsoft.com/en-us/dotnet/core/testing/unit-testing-fsharp-with-nunit}
			\end{itemize}
		\end{itemize}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Что писать}
		\framesubtitle{AppVeyor, .NET Framework}
		\begin{minted}{yaml}
image: Visual Studio 2017 

before_build: 
    - nuget restore myCoolHomework/Homework.sln

build: 
    project: myCoolHomework/Homework.sln

test_script: 
    - nunit3-console myCoolHomework/Homework.sln \ 
        --result=myresults.xml;format=AppVeyor
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Что писать}
		\framesubtitle{Travis, .NET Core}
		\begin{minted}{yaml}
language: csharp

mono: none
dotnet: 2.1

before_build: 
    - nuget restore myCoolHomework/Homework.sln

build: 
    - dotnet build myCoolHomework/Homework.sln

test_script: 
    - dotnet test myCoolHomework/Homework.sln
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Что писать}
		\framesubtitle{Travis, .NET Framework}
		\begin{small}
			\begin{minted}{yaml}
language: csharp

install:
  - nuget restore myCoolHomework/Homework.sln
  - nuget install NUnit.Console -Version 3.9.0 -OutputDirectory testrunner

script:
  - msbuild /p:Configuration=Release myCoolHomework/Homework.sln
  - mono ./testrunner/NUnit.ConsoleRunner.3.9.0/tools/nunit3-console.exe \
        .myCoolHomework/Homework.sln
			\end{minted}
		\end{small}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{А как же другие домашки?}
		\framesubtitle{AppVeyor, .NET Core}
		\begin{minted}{yaml}
image: Visual Studio 2017 

environment:
    matrix:
        - SOLUTION: myCoolHomework/Homework1.sln
        - SOLUTION: myCoolHomework2/Homework2.sln

before_build: 
    - nuget restore %SOLUTION%

build: 
    project: %SOLUTION%

test_script: 
    - dotnet test %SOLUTION%
		\end{minted}
	\end{frame}

	\begin{frame}[fragile]
		\frametitle{Автоматизация}
		\framesubtitle{AppVeyor, .NET Core}
		\begin{minted}{yaml}
image: Visual Studio 2017 

build_script: 
    - For /R %%I in (*.sln) do dotnet test %%I

test: off
		\end{minted}
	\end{frame}

\end{document}