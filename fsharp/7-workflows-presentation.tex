\documentclass[xetex,mathserif,serif]{beamer}
\usepackage{polyglossia}
\setdefaultlanguage[babelshorthands=true]{russian}
\usepackage{listings}
\usepackage{tabu}

\useoutertheme{infolines}

\usepackage{fontspec}
\setmainfont{FreeSans}
\newfontfamily{\russianfonttt}{FreeSans}

\setbeamertemplate{blocks}[rounded][shadow=false]
\setbeamercolor*{block title example}{fg=green!50!black,bg=green!20}
\setbeamercolor*{block body example}{fg=black,bg=green!10}

\setbeamercolor*{block title alerted}{fg=red!50!black,bg=red!20}
\setbeamercolor*{block body alerted}{fg=black,bg=red!10}

\lstset{language=Caml,basicstyle=\small\normalfont,keywordstyle=\color{red},showstringspaces=false}

\lstset{emph={member,float,int,static,open,class,val,new,inherit,abstract,override,default,base,interface,int,bool,unit,string,module,struct,namespace,return,public,throw},emphstyle={\color{red}},deletekeywords={value}}

\DeclareMathSymbol{\mlq}{\mathord}{operators}{``}
\DeclareMathSymbol{\mrq}{\mathord}{operators}{`'}

\title{Вычислительные выражения в F\#}
\subtitle{Computation Expressions, Workflows}
\author{Юрий Литвинов}
\date{25.03.2016г}

\begin{document}
	
	\frame{\titlepage}
	
	\begin{frame}
		\frametitle{Что это и зачем нужно}
        \framesubtitle{<<a monad is a monoid in the category of endofunctors, what's the problem?>>}
        \begin{itemize}
            \item Механизм управления процессом вычислений
            \item В функциональных языках --- единственный способ определить порядок вычислений
            \item Зачастую --- нетривиальным образом (Async)
            \item Способ не писать кучу вспомогательного кода (сродни АОП)
            \item В теории ФП они называются монадами
            \item На самом деле, синтаксический сахар
        \end{itemize}
	\end{frame}	

	\begin{frame}
		\frametitle{Пример}
        \framesubtitle{Классический пример с делением на 0}
        Сопротивление сети из параллельных резисторов:
        $$1/R = 1/R_1 + 1/R_2 + 1/R_3$$
        $R_1$, $R_2$ и $R_3$ могут быть 0. Что делать?
        \begin{itemize}
            \item Бросать исключение --- плохо
            \item Использовать option --- много работы, но попробуем
        \end{itemize}
	\end{frame}	

	\begin{frame}[fragile]
		\frametitle{Реализация вручную}
        \framesubtitle{divide}
      		\begin{exampleblock}{F\#}
      			\begin{lstlisting}
let divide x y =
    match y with
    | 0.0 -> None
    | _ -> Some (x / y)
\end{lstlisting}
\end{exampleblock}
\end{frame}	

	\begin{frame}[fragile]
		\frametitle{Реализация вручную}
        \framesubtitle{Само вычисление}
      		\begin{exampleblock}{F\#}
      			\begin{lstlisting}
let resistance r1 r2 r3 =
    let r1' = divide 1.0 r1
    match r1' with
    | None -> None
    | Some x -> let r2' = divide 1.0 r2
        match r2' with
        | None -> None
        | Some y -> let r3' = divide 1.0 r3
            match r3' with
            | None -> None
            | Some z -> let r = divide 1.0 (x + y + z)
                        r
\end{lstlisting}
\end{exampleblock}
\end{frame}	

	\begin{frame}[fragile]
		\frametitle{То же самое, через Workflow Builder}
      		\begin{exampleblock}{F\#}
      			\begin{lstlisting}
type MaybeBuilder() =
    member this.Bind(x, f) = 
        match x with
        | None -> None
        | Some a -> f a
    member this.Return(x) = 
        Some x
   
let maybe = new MaybeBuilder()
\end{lstlisting}
\end{exampleblock}
\end{frame}	

	\begin{frame}[fragile]
		\frametitle{Само вычисление}
      		\begin{exampleblock}{F\#}
      			\begin{lstlisting}
let resistance r1 r2 r3 = 
    maybe {
        let! r1' = divide 1.0 r1
        let! r2' = divide 1.0 r2
        let! r3' = divide 1.0 r3
        let! r = divide 1.0 (r1' + r2' + r3')
        return r
    }
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Некоторые синтаксические "похожести"}
		\framesubtitle{seq --- это тоже Computation Expression}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let daysOfTheYear =
    seq {
        let months =
            ["Jan"; "Feb"; "Mar"; "Apr"; "May"; "Jun";
             "Jul"; "Aug"; "Sep"; "Oct"; "Nov"; "Dec"]
        let daysInMonth month =
            match month with
            | "Feb" -> 28
            | "Apr" | "Jun" | "Sep" | "Nov" -> 30
            | _ -> 31
        for month in months do
            for day = 1 to daysInMonth month do
                yield (month, day)
    }
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Ещё один пример}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let debug x = printfn "value is %A" x

let withDebug = 
    let a = 1
    debug a
    let b = 2
    debug b
    let c = a + b
    debug c
    c
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{То же самое с Workflow}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
type DebugBuilder() =
    member this.Bind(x, f) = 
        debug x 
        f x
    member this.Return(x) = x

let debugFlow = DebugBuilder ()

let withDebug = debugFlow {
        let! a = 1
        let! b = 2
        let! c = a + b
        return c
    }
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}
		\frametitle{Что происходит}
		\framesubtitle{Как оно устроено внутри}
		\begin{itemize}
    		\item Bind создаёт цепочку continuation passing style-функций, возможно, с побочными эффектами
    		\item Есть тип-обёртка (или монадический тип), в котором хранится состояние вычисления
    		\item let! вызывает Bind, return --- Return, Bind принимает обёрнутое значение и функцию-continuation, return по необёрнутому значению делает обёрнутое
		\end{itemize}
    \end{frame}

	\begin{frame}[fragile]
		\frametitle{Отступление про CPS}
		\framesubtitle{C\#, код без CPS}
   		\begin{exampleblock}{C\#}
   			\begin{lstlisting}
public int Divide(int a, int b)
{
    if (b == 0)
    {
        throw new InvalidOperationException("div by 0");
    }
    else
    {
        return a / b;
    }
}
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{C\#, то же с CPS}
   		\begin{exampleblock}{C\#}
   			\begin{lstlisting}
public T Divide<T>(int a, int b, Func<T> ifZero
        , Func<int, T> ifSuccess)
{
    if (b == 0)
    {
        return ifZero();
    }
    else
    {
        return ifSuccess(a / b);
    }
}
\end{lstlisting}
\end{exampleblock}
Вызывающий решает, что делать, а не вызываемый.
\end{frame}

	\begin{frame}[fragile]
		\frametitle{То же на F\#}
		Без CPS:
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let divide a b =
    if (b = 0) 
    then invalidOp "div by 0"
    else (a / b)
\end{lstlisting}
\end{exampleblock}
        С CPS:
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let divide ifZero ifSuccess a b = 
    if (b = 0) 
    then ifZero()
    else ifSuccess (a / b)
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Примеры}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let ifZero1 () = printfn "bad"
let ifSuccess1 x = printfn "good %i" x
let divide1  = divide ifZero1 ifSuccess1

let ifZero2() = None
let ifSuccess2 x = Some x
let divide2  = divide ifZero2 ifSuccess2

let ifZero3() = failwith "div by 0"
let ifSuccess3 x = x
let divide3  = divide ifZero3 ifSuccess3
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{let, <<многословный>> синтаксис}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let x = something
\end{lstlisting}
\end{exampleblock}
равносильно
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let x = something in [ выражение c x ]
\end{lstlisting}
\end{exampleblock}
например,
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let x = 1 in   
  let y = 2 in 
    let z = x + y in
       z    
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{let и лямбды}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
fun x -> [ выражение c x ]
\end{lstlisting}
\end{exampleblock}
или
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
something |> (fun x -> [ выражение c x ])
\end{lstlisting}
\end{exampleblock}
и обращаем внимание, что:
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let x = someExpression in [ выражение c x ]
someExpression |> (fun x -> [ выражение c x ])
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{let и CPS}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let x = 1 in   
  let y = 2 in 
    let z = x + y in
       z    
\end{lstlisting}
\end{exampleblock}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
1 |> (fun x ->
  2 |> (fun y -> 
     x + y |> (fun z -> 
       z)))
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Теперь вспомним про Workflow-ы}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let pipeInto expr f
  expr |> f
\end{lstlisting}
\end{exampleblock}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
pipeInto (1, fun x ->
  pipeInto (2, fun y -> 
    pipeInto (x + y, fun z -> 
       z)))
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Зачем}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let pipeInto (expr, f) =
   printfn "expression is %A" expr 
   expr |> f 
\end{lstlisting}
\end{exampleblock}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
pipeInto (1, fun x ->
  pipeInto (2, fun y -> 
    pipeInto (x + y, fun z -> 
       z)))
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{То же самое с Workflow}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
type DebugBuilder() =
    member this.Bind(x, f) = 
        debug x 
        f x
    member this.Return(x) = x

let debugFlow = DebugBuilder ()

let withDebug = debugFlow {
        let! a = 1
        let! b = 2
        let! c = a + b
        return c
    }
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Более сложный пример, с делением}
		\framesubtitle{pipeInto, которая потом будет Bind}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let pipeInto (expr, f) =
   match expr with
   | None -> 
       None
   | Some x -> 
       x |> f
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Более сложный пример, с делением}
		\framesubtitle{Сам процесс}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let resistance r1 r2 r3 = 
    let a = divide 1.0 r1
    pipeInto (a, fun a' ->
        let b = divide 1.0 r2
        pipeInto (b, fun b' ->
            let c = divide 1.0 r3
            pipeInto (c, fun c' ->
                let r = divide 1.0 (a + b + c)
                pipeInto (r, fun r' ->
                    Some r
                ))))
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Уберём временные let-ы}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let resistance r1 r2 r3 = 
    pipeInto (divide 1.0 r1, fun a ->
        pipeInto (divide 1.0 r2, fun b ->
            pipeInto (divide 1.0 r3, fun c ->
                pipeInto (divide 1.0 (a + b + c), fun r ->
                    Some r
                )))
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{И отформатируем}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let resistance r1 r2 r3 = 
    pipeInto (divide 1.0 r1, fun a ->
    pipeInto (divide 1.0 r2, fun b ->
    pipeInto (divide 1.0 r3, fun c ->
    pipeInto (divide 1.0 (a + b + c) , fun r ->
    Some r
    ))))
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Сравним с оригиналом}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let resistance r1 r2 r3 = 
    maybe {
        let! r1' = divide 1.0 r1
        let! r2' = divide 1.0 r2
        let! r3' = divide 1.0 r3
        let! r = divide 1.0 (r1' + r2' + r3')
        return r
    }
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Подробнее про Bind}
		\begin{itemize}
    		\item Bind : M<'T> * ('T -> M<'U>) -> M<'U>
    		\item Return : 'T -> M<'T>
		\end{itemize}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let! x = 1 in x * 2
\end{lstlisting}
\end{exampleblock}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
builder.Bind(1, (fun x -> x * 2))
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Инфиксное определение Bind}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let (>>=) m f = pipeInto(m, f)

let workflow = 
    1 >>= (+) 2 >>= (*) 42 >>= id
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Option.bind и maybe}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
module Option = 
    let bind f m =
       match m with
       | None -> 
           None
       | Some x -> 
           x |> f 

type MaybeBuilder() =
    member this.Bind(m, f) = Option.bind f m
    member this.Return(x) = Some x
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Содержимое типа-обёртки может иметь разный тип}
		\framesubtitle{Пример, серия запросов к БД}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
type DbResult<'a> = 
    | Success of 'a
    | Error of string

type CustomerId =  CustomerId of string
type OrderId =  OrderId of int
type ProductId =  ProductId of string
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Пример, запросы}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let getCustomerId name =
    if (name = "") 
    then Error "getCustomerId failed"
    else Success (CustomerId "Cust42")

let getLastOrderForCustomer (CustomerId custId) =
    if (custId = "") 
    then Error "getLastOrderForCustomer failed"
    else Success (OrderId 123)

let getLastProductForOrder (OrderId orderId) =
    if (orderId  = 0) 
    then Error "getLastProductForOrder failed"
    else Success (ProductId "Product456")
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Общение с БД вручную}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let product = 
    let r1 = getCustomerId "Alice"
    match r1 with 
    | Error e -> Error e
    | Success custId ->
        let r2 = getLastOrderForCustomer custId 
        match r2 with 
        | Error e -> Error e
        | Success orderId ->
            let r3 = getLastProductForOrder orderId 
            match r3 with 
            | Error e -> Error e
            | Success productId ->
                printfn "Product is %A" productId
                r3
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Builder}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
type DbResultBuilder() =

    member this.Bind(m, f) = 
        match m with
        | Error e -> Error e
        | Success a -> 
            printfn "Successful: %A" a
            f a

    member this.Return(x) = 
        Success x

let dbresult = new DbResultBuilder()
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Workflow}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let product = 
    dbresult {
        let! custId = getCustomerId "Alice"
        let! orderId = getLastOrderForCustomer custId
        let! productId = getLastProductForOrder orderId 
        printfn "Product is %A" productId
        return productId
    }
printfn "%A" product
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Композиция Workflow-ов}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let subworkflow1 = myworkflow { return 42 }
let subworkflow2 = myworkflow { return 43 }

let aWrappedValue = 
    myworkflow {
        let! unwrappedValue1 = subworkflow1
        let! unwrappedValue2 = subworkflow2
        return unwrappedValue1 + unwrappedValue2
        }
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Вложенные Workflow-ы}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let aWrappedValue = 
    myworkflow {
        let! unwrappedValue1 = myworkflow {
            let! x = myworkflow { return 1 }
            return x
            }
        let! unwrappedValue2 = myworkflow {
            let! y = myworkflow { return 2 }
            return y
            }
        return unwrappedValue1 + unwrappedValue2
        }
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{ReturnFrom}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
type MaybeBuilder() =
    member this.Bind(m, f) = Option.bind f m
    member this.Return(x) = 
        printfn "Wrapping a raw value into an option"
        Some x
    member this.ReturnFrom(m) = 
        printfn "Returning an option directly"
        m

let maybe = new MaybeBuilder()
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Пример}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
maybe { return 1  }

maybe { return! (Some 2)  }
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Зачем это}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
maybe {
    let! x = divide 24 3
    let! y = divide x 2
    return y 
}    

maybe {
    let! x = divide 24 3
    return! divide x 2  
}
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Первый закон монад}
		\begin{itemize}
    		\item Bind и Return должны быть взаимно обратны
		\end{itemize}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
myworkflow {
    let originalUnwrapped = something
    let wrapped = myworkflow { return originalUnwrapped }
    let! newUnwrapped = wrapped
    assertEqual newUnwrapped originalUnwrapped 
}
myworkflow {
    let originalWrapped = something
    let newWrapped = myworkflow { 
        let! unwrapped = originalWrapped
        return unwrapped
    }
    assertEqual newWrapped originalWrapped
}
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Второй закон монад}
		\begin{itemize}
    		\item Композиция должна быть консистентной
		\end{itemize}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let result1 = myworkflow { 
    let! x = originalWrapped
    let! y = f x 
    return! g y  
}
let result2 = myworkflow { 
    let! y = myworkflow { 
        let! x = originalWrapped
        return! f x
    }
    return! g y
}
assertEqual result1 result2
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}
		\frametitle{Какие ещё методы есть у WorkflowBuilder}
		\begin{small}
			\begin{tabu} {| X[0.5 l p] | X[1 l p] | X[1 l p] |}
				\tabucline-
				Имя                               & Тип                  & Описание           \\
				\tabucline-
				\everyrow{\tabucline-}
				Delay                    & (unit -> M<'T>) -> M<'T>  & Превращает в функцию   \\
				Run                      & M<'T> -> M<'T>  & Исполняет вычисление   \\
				Combine                   & M<'T> * M<'T> -> M<'T>  & Последовательное исполнение\\
				For                   & seq<'T> * ('T -> M<'U>) -> M<'U>  & Цикл for \\

				TryWith                   & M<'T> * (exn -> M<'T>) -> M<'T>  & Блок try with \\
				TryFinally            & M<'T> * (unit -> unit) -> M<'T>  & Блок finally \\
				Using                   & 'T * ('T -> M<'U>) -> M<'U> when 'U :> IDisposable  & use \\
				While                   & (unit -> bool) * M<'T> -> M<'T>  & Цикл while \\
				Yield                   & 'T -> M<'T>  & yield или -> \\
				YieldFrom                   & M<'T> -> M<'T>  & yield! или ->> \\
				Zero                   & unit -> M<'T>  & Обёрнутое () \\
   			\end{tabu}
		\end{small}
	\end{frame}

	\begin{frame}
		\frametitle{Моноиды}
		\framesubtitle{Немного алгебры}
		Множество с бинарной операцией
		\begin{itemize}
    		\item Замкнутость относительно операции
    		\item Ассоциативность
    		\item Наличие нейтрального элемента
		\end{itemize}
		Например, [a] @ [b] = [a; b]
    \end{frame}

	\begin{frame}[fragile]
		\frametitle{Пример}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
type OrderLine = {Quantity : int; Total : float}

let orderLines = [
    {Quantity = 2; Total = 19.98};
    {Quantity = 1; Total = 1.99};
    {Quantity = 2; Total = 3.98}; ]
    
let addLine line1 line2 =
    {Quantity = line1.Quantity + line2.Quantity; 
     Total = line1.Total + line2.Total}
     
orderLines |> List.reduce addLine
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}
		\frametitle{Эндоморфизмы}
		Эндоморфизм --- функция, у которой тип входного значения совпадает с типом выходного
		
		\vspace{1cm}
		Множество функций + композиция --- моноид, если функции --- эндоморфизмы
    \end{frame}

	\begin{frame}[fragile]
		\frametitle{Пример}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let plus1 x = x + 1
let times2 x = x * 2
let subtract42 x = x - 42

let functions = [
    plus1;
    times2;
    subtract42 ]
    
let newFunction = functions |> List.reduce (>>)

printfn "%d" <| newFunction 20
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Bind}
		Option.bind : ('T $\to$ 'U option) $\to$ 'T option $\to$ 'U option

		--- частично применённый Bind --- эндоморфизм (если 'T и 'U совпадают)
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
let bindFns = [
    Option.bind (fun x -> if x > 1 then 
                              Some (x * 2) 
                          else None);
    Option.bind (fun x -> if x < 10 then Some x else None)
]

let bindAll = 
    bindFns |> List.reduce (>>)
    
Some 4 |> bindAll
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}[fragile]
		\frametitle{Не только эндоморфизмы могут образовать моноид}
   		\begin{exampleblock}{F\#}
   			\begin{lstlisting}
type Predicate<'A> = 'A -> bool

let predAnd p1 p2 x = 
    if p1 x 
    then p2 x
    else false
    
let predicates = [isMoreThan10Chars; isMixedCase; isNotDictionaryWord]

let combinePredicates = predicates |> List.reduce predAnd
\end{lstlisting}
\end{exampleblock}
\end{frame}

	\begin{frame}
		\frametitle{Монады}
		\framesubtitle{Workflow-ы, Computational Expressions}
		\begin{itemize}
    		\item Замкнуты
    		\item Композиция ассоциативна (второй закон монад)
    		\item Нейтральный элемент (Return, первый закон монад)
		\end{itemize}
		<<a monad is a monoid in the category of endofunctors, what's the problem?>>
    \end{frame}

	\begin{frame}
		\frametitle{Полезные ссылки}
		\framesubtitle{Откуда взяты примеры}
		\begin{small}
    		\begin{itemize}
        		\item \url{https://fsharpforfunandprofit.com/series/computation-expressions.html}
        		\item \url{http://www.slideshare.net/ScottWlaschin/fp-patterns-buildstufflt}
    		\end{itemize}
		\end{small}
    \end{frame}

\end{document}
